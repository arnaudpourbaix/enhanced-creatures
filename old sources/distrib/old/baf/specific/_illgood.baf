///////////////////////////////////////////////////////////////////////
// Demon Knight (targetting his mirror)
///////////////////////////////////////////////////////////////////////


/////////////////////////////////////////////////////////////////////
// Just after resting, everything is reset and creature is healed
/////////////////////////////////////////////////////////////////////
IF
  GlobalTimerExpired("ja#PrepTimer","LOCALS")
  Global("ja#initialize","LOCALS",1)
  !See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    Rest()
    ApplySpellRES("ja#HEAL",Myself)
    SetGlobal("ja#initialize","LOCALS",0)
END


///////////////////////////////////////////////////////////////////////
// Initialize variables & timers
///////////////////////////////////////////////////////////////////////
IF
  Global("ja#initialize","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("ja#initialize","LOCALS",1)
    SetGlobal("ja#combat","LOCALS",0)
    SetGlobal("DeathKnightDispel","LOCALS",0)
    SetGlobal("ja#powerwordkill","LOCALS",0)
    SetGlobal("ja#powerwordstun","LOCALS",0)
    SetGlobal("ja#symbolfear","LOCALS",0)
    SetGlobal("ja#fireball","LOCALS",0)
    SetGlobalTimer("ja#PrepTimer","LOCALS",2400)
END


/////////////////////////////////////////////////////////////////////
// See an enemy
/////////////////////////////////////////////////////////////////////
IF
  See("gooddeat")
  Global("ja#combat","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("ja#combat","LOCALS",1)
END


IF
  Die()
THEN
  RESPONSE #100
    ReallyForceSpellDead(Myself,FIREBALL_DEATH)
END


IF
  Dead("gooddeat")
THEN
  RESPONSE #100
    ChangeAIScript("_EATHINI",OVERRIDE)
END


/////////////////////////////////////////////////////////////////////
// Do nothing if no enemy is detected and no combat started
/////////////////////////////////////////////////////////////////////
IF
  Global("ja#combat","LOCALS",0)
  !Detect(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    NoAction()
END


/////////////////////////////////////////////////////////////////////
// Are there any enemies that try to escape, follow them
// It's not about sight but memory (I saw that enemy going in that dir.)
/////////////////////////////////////////////////////////////////////
IF
  Allegiance(Myself,ENEMY)
  InMyArea("gooddeat")
  HPGT("gooddeat",0)
  !GlobalTimerNotExpired("ja#teleport","LOCALS")
  OR(2)
    !See("gooddeat")
    Global("ja#nothingtodo","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("ja#nothingtodo","LOCALS",0)
        SetGlobalTimer("ja#teleport","LOCALS",6)
    ForceSpellRES("SPWI931","gooddeat")
END


/////////////////////////////////////////////////////////////////////
// Do nothing if no enemy is detected
/////////////////////////////////////////////////////////////////////
IF
  !Detect(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    NoAction()
END


/////////////////////////////////////////////////////////////////////
// Demon fear
/////////////////////////////////////////////////////////////////////
IF
  See("gooddeat")
  !StateCheck(LastSeenBy(Myself),STATE_PANIC)
  Range(LastSeenBy(Myself),10)
  !GlobalTimerNotExpired("DemFear","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("DemFear","LOCALS",15)
    ForceSpell(LastSeenBy(Myself),DEMON_FEAR)
END


/////////////////////////////////////////////////////////////////////
// Power word kill
/////////////////////////////////////////////////////////////////////
IF
  See("gooddeat")
  Global("ja#powerwordkill","LOCALS",0)
  !GlobalTimerNotExpired("ja#round","LOCALS")
  HPLT(LastSeenBy(Myself),60)
THEN
  RESPONSE #100
    SetGlobalTimer("ja#round","LOCALS",6)
    SetGlobal("ja#powerwordkill","LOCALS",1)
    ForceSpell(LastSeenBy(Myself),WIZARD_POWER_WORD_KILL)
END


/////////////////////////////////////////////////////////////////////
// Power word stun
/////////////////////////////////////////////////////////////////////
IF
  See("gooddeat")
  Global("ja#powerwordstun","LOCALS",0)
  !GlobalTimerNotExpired("ja#round","LOCALS")
  HPLT(LastSeenBy(Myself),60)
  !StateCheck(LastSeenBy(Myself),STATE_STUNNED)
THEN
  RESPONSE #100
    SetGlobalTimer("ja#round","LOCALS",6)
    SetGlobal("ja#powerwordstun","LOCALS",1)
    ForceSpell(LastSeenBy(Myself),WIZARD_POWER_WORD_STUN)
END


/////////////////////////////////////////////////////////////////////
// Attack mirror
/////////////////////////////////////////////////////////////////////
IF
  See("gooddeat")
THEN
  RESPONSE #100
    AttackOneRound("gooddeat")
END
