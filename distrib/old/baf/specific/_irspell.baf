///////////////////////////////////////////////////////////////////////
// Sirine
///////////////////////////////////////////////////////////////////////


/////////////////////////////////////////////////////////////////////
// Just after resting, everything is reset and creature is healed
/////////////////////////////////////////////////////////////////////
IF
  GlobalTimerExpired("ja#PrepTimer","LOCALS")
  Global("ja#initialize","LOCALS",1)
  !See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    Rest()
    ApplySpellRES("ja#HEAL",Myself)
    SetGlobal("ja#initialize","LOCALS",0)
END


///////////////////////////////////////////////////////////////////////
// Initialize variables & timers
///////////////////////////////////////////////////////////////////////
IF
  Global("ja#initialize","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("ja#initialize","LOCALS",1)
    SetGlobal("ja#combat","LOCALS",0)
    SetGlobal("ja#buffdone","LOCALS",0)
    SetGlobal("ja#polymorph","LOCALS",0)
    SetGlobalTimer("ja#PrepTimer","LOCALS",2400)
    ChangeClass(Myself,MAGE)
END


///////////////////////////////////////////////////////////////////////
// Threat Detection
//
// Attacked? Spell cast on me? Someone shouted a warning?
///////////////////////////////////////////////////////////////////////
IF
  !Allegiance(Myself,ENEMY)
  OR(12)
    AttackedBy([GOODCUTOFF.0.0.0.0.SUMMONED],DEFAULT)
    AttackedBy([PC],DEFAULT)
    AttackedBy([FAMILIAR],DEFAULT)
    AttackedBy([ALLY],DEFAULT)
    AttackedBy([CONTROLLED],DEFAULT)
    AttackedBy([CHARMED],DEFAULT)
    SpellCastOnMe([GOODCUTOFF.0.0.0.0.SUMMONED],0)
    SpellCastOnMe([PC],0)
    SpellCastOnMe([FAMILIAR],0)
    SpellCastOnMe([ALLY],0)
    SpellCastOnMe([CONTROLLED],0)
    SpellCastOnMe([CHARMED],0)
THEN
  RESPONSE #100
    Enemy()
    Shout(95)
END

IF
  Heard([ANYONE],95)
  Allegiance(Myself,NEUTRAL)
THEN
  RESPONSE #100
    Enemy()
    SetGlobal("ja#combat","LOCALS",1)
    MoveToObject(LastHeardBy(Myself))
END

IF
  Heard([ANYONE],95)
  !See(NearestEnemyOf(Myself))  
THEN
  RESPONSE #100
    SetGlobal("ja#combat","LOCALS",1)
    MoveToObject(LastHeardBy(Myself))
END


/////////////////////////////////////////////////////////////////////
// See an enemy
/////////////////////////////////////////////////////////////////////
IF
  See(NearestEnemyOf(Myself))
  Global("ja#combat","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("ja#combat","LOCALS",1)
    Shout(95)
END


/////////////////////////////////////////////////////////////////////
// Do nothing if no enemy is detected and no combat started
/////////////////////////////////////////////////////////////////////
IF
  Global("ja#combat","LOCALS",0)
  !Detect(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    NoAction()
END


/////////////////////////////////////////////////////////////////////
// Unable to cast a spell
/////////////////////////////////////////////////////////////////////
IF
  Global("ja#disable_spellcasting","LOCALS",0)
  OR(2)
    StateCheck(Myself,STATE_SILENCED)
    CheckStatGT(Myself,69,SPELLFAILUREPRIEST)
THEN
  RESPONSE #100
    SetGlobal("ja#disable_spellcasting","LOCALS",1)
END


/////////////////////////////////////////////////////////////////////
// Able to cast a spell
/////////////////////////////////////////////////////////////////////
IF
  Global("ja#disable_spellcasting","LOCALS",1)
  !StateCheck(Myself,STATE_SILENCED)
  CheckStatLT(Myself,70,SPELLFAILUREPRIEST)
  !CheckStat(Myself,1,POLYMORPH_SELF)
THEN
  RESPONSE #100
    SetGlobal("ja#disable_spellcasting","LOCALS",0)
END


/////////////////////////////////////////////////////////////////////
// Improve invisibility
/////////////////////////////////////////////////////////////////////
IF
  !GlobalTimerNotExpired("ja#round","LOCALS")
  Global("ja#disable_spellcasting","LOCALS",0)
  HaveSpell(WIZARD_IMPROVED_INVISIBILITY)
THEN
  RESPONSE #100
    SetGlobalTimer("ja#round","LOCALS",6)
    Spell(Myself,WIZARD_IMPROVED_INVISIBILITY)
END


/////////////////////////////////////////////////////////////////////
// Minor globe of invulnerability
/////////////////////////////////////////////////////////////////////
IF
  !GlobalTimerNotExpired("ja#round","LOCALS")
  Global("ja#disable_spellcasting","LOCALS",0)
  HaveSpell(WIZARD_MINOR_GLOBE_OF_INVULNERABILITY)
THEN
  RESPONSE #100
    SetGlobalTimer("ja#round","LOCALS",6)
    Spell(Myself,WIZARD_MINOR_GLOBE_OF_INVULNERABILITY)
END


/////////////////////////////////////////////////////////////////////
// Back to human form
/////////////////////////////////////////////////////////////////////
IF
  Global("ja#polymorph","LOCALS",1)  
  CheckStat(Myself,0,POLYMORPH_SELF) 
THEN
  RESPONSE #100
    SetGlobal("ja#polymorph","LOCALS",0)
    SetGlobal("ja#disable_spellcasting","LOCALS",0)
END


/////////////////////////////////////////////////////////////////////
// Polymorph self
/////////////////////////////////////////////////////////////////////
IF
  !GlobalTimerNotExpired("ja#round","LOCALS")
  Global("ja#disable_spellcasting","LOCALS",0)
  CheckStat(Myself,0,POLYMORPH_SELF) 
  HaveSpell(WIZARD_POLYMORPH_SELF)
  OR(2)
    StateCheck(Myself,STATE_INVISIBLE)
    !HaveSpellRES("ja#SIRIN")
THEN
  RESPONSE #100
    SetGlobalTimer("ja#round","LOCALS",6)
    Spell(Myself,WIZARD_POLYMORPH_SELF)
    SetGlobal("ja#polymorph","LOCALS",1)
    SetGlobalTimer("ja#polyform","LOCALS",9)
END


/////////////////////////////////////////////////////////////////////
// Polymorph forms
/////////////////////////////////////////////////////////////////////
IF
  !GlobalTimerNotExpired("ja#round","LOCALS")
  !GlobalTimerNotExpired("ja#polyform","LOCALS")
  CheckStat(Myself,1,POLYMORPH_SELF)
THEN
  RESPONSE #100
    SetGlobalTimer("ja#polyform","LOCALS",24)
    SetGlobalTimer("ja#round","LOCALS",6)
    ForceSpellRES("spwi495",Myself)  // Sword spider
    SetGlobal("ja#disable_spellcasting","LOCALS",1)
  RESPONSE #100
    SetGlobalTimer("ja#polyform","LOCALS",24)
    SetGlobalTimer("ja#round","LOCALS",6)
    ForceSpellRES("spwi494",Myself)  // Ogre
    SetGlobal("ja#disable_spellcasting","LOCALS",1)
  RESPONSE #100
    SetGlobalTimer("ja#polyform","LOCALS",24)
    SetGlobalTimer("ja#round","LOCALS",6)
    ForceSpellRES("spwi497",Myself)  // Brown bear
    SetGlobal("ja#disable_spellcasting","LOCALS",1)
  RESPONSE #100
    SetGlobalTimer("ja#polyform","LOCALS",24)
    SetGlobalTimer("ja#round","LOCALS",6)
    ForceSpellRES("spwi496",Myself)  // Mustard jelly
    SetGlobal("ja#disable_spellcasting","LOCALS",1)
  RESPONSE #100
    SetGlobalTimer("ja#polyform","LOCALS",24)
    SetGlobalTimer("ja#round","LOCALS",6)
    ForceSpellRES("spwi499",Myself)  // Wolf
    SetGlobal("ja#disable_spellcasting","LOCALS",1)
  RESPONSE #100
    SetGlobalTimer("ja#polyform","LOCALS",24)
    SetGlobalTimer("ja#round","LOCALS",6)
    ForceSpellRES("spwi490",Myself)  // Natural form
    SetGlobal("ja#disable_spellcasting","LOCALS",0)
END


/////////////////////////////////////////////////////////////////////
// If all buffing spells are done, combat can start
/////////////////////////////////////////////////////////////////////
IF
  Global("ja#buffdone","LOCALS",0)
  GlobalTimerExpired("ja#round","LOCALS")
  !HaveSpell(WIZARD_IMPROVED_INVISIBILITY)
  OR(2)
    !HaveSpell(WIZARD_POLYMORPH_SELF)  
    !StateCheck(Myself,STATE_INVISIBLE)
THEN
  RESPONSE #100
    SetGlobal("ja#buffdone","LOCALS",1)
END


/////////////////////////////////////////////////////////////////////
// Not ready to attack, wait
/////////////////////////////////////////////////////////////////////
IF
  Global("ja#buffdone","LOCALS",0)
THEN
  RESPONSE #100
    NoAction()
END


/////////////////////////////////////////////////////////////////////
// Are there any enemies that try to escape, follow them
// It's not about sight but memory (I saw that enemy going in that dir.)
/////////////////////////////////////////////////////////////////////
IF
  Allegiance(Myself,ENEMY)
  ActionListEmpty()
  !See(NearestEnemyOf(Myself))
  !StateCheck(LastAttackerOf(Myself),STATE_INVISIBLE)
  Range(LastAttackerOf(Myself),70)
THEN
  RESPONSE #100
    MoveToObject(LastAttackerOf(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  ActionListEmpty()
  !See(NearestEnemyOf(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  Range(LastSeenBy(Myself),70)
THEN
  RESPONSE #100
    MoveToObject(LastSeenBy(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  ActionListEmpty()
  !See(NearestEnemyOf(Myself))
  !StateCheck(Player1,STATE_INVISIBLE)
  Range(Player1,70)
THEN
  RESPONSE #100
    MoveToObject(Player1)
END

IF
  Allegiance(Myself,ENEMY)
  ActionListEmpty()
  !See(NearestEnemyOf(Myself))
  !StateCheck(Player2,STATE_INVISIBLE)
  Range(Player2,70)
THEN
  RESPONSE #100
    MoveToObject(Player2)
END

IF
  Allegiance(Myself,ENEMY)
  ActionListEmpty()
  !See(NearestEnemyOf(Myself))
  !StateCheck(Player3,STATE_INVISIBLE)
  Range(Player3,70)
THEN
  RESPONSE #100
    MoveToObject(Player3)
END

IF
  Allegiance(Myself,ENEMY)
  ActionListEmpty()
  !See(NearestEnemyOf(Myself))
  !StateCheck(Player4,STATE_INVISIBLE)
  Range(Player4,70)
THEN
  RESPONSE #100
    MoveToObject(Player4)
END

IF
  Allegiance(Myself,ENEMY)
  ActionListEmpty()
  !See(NearestEnemyOf(Myself))
  !StateCheck(Player5,STATE_INVISIBLE)
  Range(Player5,70)
THEN
  RESPONSE #100
    MoveToObject(Player5)
END

IF
  Allegiance(Myself,ENEMY)
  ActionListEmpty()
  !See(NearestEnemyOf(Myself))
  !StateCheck(Player6,STATE_INVISIBLE)
  Range(Player6,70)
THEN
  RESPONSE #100
    MoveToObject(Player6)
END


/////////////////////////////////////////////////////////////////////
// Do nothing if no enemy is detected
/////////////////////////////////////////////////////////////////////
IF
  !Detect(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    NoAction()
END


/////////////////////////////////////////////////////////////////////
// Find a target to charm/dominate
/////////////////////////////////////////////////////////////////////
IF
  !Race(SixthNearestEnemyOf(Myself),ELF)
  !CheckStat(SixthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(SixthNearestEnemyOf(Myself),0,MINORGLOBE)
  !CheckStat(SixthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  !CheckStat(SixthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStat(SixthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_BLOCKING)
  !StateCheck(SixthNearestEnemyOf(Myself),STATE_SLOWED)
  !StateCheck(SixthNearestEnemyOf(Myself),STATE_SLEEPING)
  !StateCheck(SixthNearestEnemyOf(Myself),STATE_STUNNED)
  !StateCheck(SixthNearestEnemyOf(Myself),STATE_PANIC)
  !StateCheck(SixthNearestEnemyOf(Myself),STATE_FEEBLEMINDED)
  !StateCheck(SixthNearestEnemyOf(Myself),STATE_BLIND)
  !CheckStatGT(SixthNearestEnemyOf(Myself),0,HELD)
  !StateCheck(SixthNearestEnemyOf(Myself),STATE_HELPLESS)
  !StateCheck(SixthNearestEnemyOf(Myself),STATE_CONFUSED)
  !StateCheck(SixthNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(SixthNearestEnemyOf(Myself),0,SANCTUARY)
  See(SixthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !Race(FifthNearestEnemyOf(Myself),ELF)
  !CheckStat(FifthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(FifthNearestEnemyOf(Myself),0,MINORGLOBE)
  !CheckStat(FifthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  !CheckStat(FifthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStat(FifthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_BLOCKING)
  !StateCheck(FifthNearestEnemyOf(Myself),STATE_SLOWED)
  !StateCheck(FifthNearestEnemyOf(Myself),STATE_SLEEPING)
  !StateCheck(FifthNearestEnemyOf(Myself),STATE_STUNNED)
  !StateCheck(FifthNearestEnemyOf(Myself),STATE_PANIC)
  !StateCheck(FifthNearestEnemyOf(Myself),STATE_FEEBLEMINDED)
  !StateCheck(FifthNearestEnemyOf(Myself),STATE_BLIND)
  !CheckStatGT(FifthNearestEnemyOf(Myself),0,HELD)
  !StateCheck(FifthNearestEnemyOf(Myself),STATE_HELPLESS)
  !StateCheck(FifthNearestEnemyOf(Myself),STATE_CONFUSED)
  !StateCheck(FifthNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(FifthNearestEnemyOf(Myself),0,SANCTUARY)
  See(FifthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !Race(FourthNearestEnemyOf(Myself),ELF)
  !CheckStat(FourthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(FourthNearestEnemyOf(Myself),0,MINORGLOBE)
  !CheckStat(FourthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  !CheckStat(FourthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStat(FourthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_BLOCKING)
  !StateCheck(FourthNearestEnemyOf(Myself),STATE_SLOWED)
  !StateCheck(FourthNearestEnemyOf(Myself),STATE_SLEEPING)
  !StateCheck(FourthNearestEnemyOf(Myself),STATE_STUNNED)
  !StateCheck(FourthNearestEnemyOf(Myself),STATE_PANIC)
  !StateCheck(FourthNearestEnemyOf(Myself),STATE_FEEBLEMINDED)
  !StateCheck(FourthNearestEnemyOf(Myself),STATE_BLIND)
  !CheckStatGT(FourthNearestEnemyOf(Myself),0,HELD)
  !StateCheck(FourthNearestEnemyOf(Myself),STATE_HELPLESS)
  !StateCheck(FourthNearestEnemyOf(Myself),STATE_CONFUSED)
  !StateCheck(FourthNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(FourthNearestEnemyOf(Myself),0,SANCTUARY)
  See(FourthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !Race(ThirdNearestEnemyOf(Myself),ELF)
  !CheckStat(ThirdNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(ThirdNearestEnemyOf(Myself),0,MINORGLOBE)
  !CheckStat(ThirdNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  !CheckStat(ThirdNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStat(ThirdNearestEnemyOf(Myself),1,POTION_OF_MAGIC_BLOCKING)
  !StateCheck(ThirdNearestEnemyOf(Myself),STATE_SLOWED)
  !StateCheck(ThirdNearestEnemyOf(Myself),STATE_SLEEPING)
  !StateCheck(ThirdNearestEnemyOf(Myself),STATE_STUNNED)
  !StateCheck(ThirdNearestEnemyOf(Myself),STATE_PANIC)
  !StateCheck(ThirdNearestEnemyOf(Myself),STATE_FEEBLEMINDED)
  !StateCheck(ThirdNearestEnemyOf(Myself),STATE_BLIND)
  !CheckStatGT(ThirdNearestEnemyOf(Myself),0,HELD)
  !StateCheck(ThirdNearestEnemyOf(Myself),STATE_HELPLESS)
  !StateCheck(ThirdNearestEnemyOf(Myself),STATE_CONFUSED)
  !StateCheck(ThirdNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(ThirdNearestEnemyOf(Myself),0,SANCTUARY)
  See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !Race(SecondNearestEnemyOf(Myself),ELF)
  !CheckStat(SecondNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(SecondNearestEnemyOf(Myself),0,MINORGLOBE)
  !CheckStat(SecondNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  !CheckStat(SecondNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStat(SecondNearestEnemyOf(Myself),1,POTION_OF_MAGIC_BLOCKING)
  !StateCheck(SecondNearestEnemyOf(Myself),STATE_SLOWED)
  !StateCheck(SecondNearestEnemyOf(Myself),STATE_SLEEPING)
  !StateCheck(SecondNearestEnemyOf(Myself),STATE_STUNNED)
  !StateCheck(SecondNearestEnemyOf(Myself),STATE_PANIC)
  !StateCheck(SecondNearestEnemyOf(Myself),STATE_FEEBLEMINDED)
  !StateCheck(SecondNearestEnemyOf(Myself),STATE_BLIND)
  !CheckStatGT(SecondNearestEnemyOf(Myself),0,HELD)
  !StateCheck(SecondNearestEnemyOf(Myself),STATE_HELPLESS)
  !StateCheck(SecondNearestEnemyOf(Myself),STATE_CONFUSED)
  !StateCheck(SecondNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(SecondNearestEnemyOf(Myself),0,SANCTUARY)
  See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !Race(NearestEnemyOf(Myself),ELF)
  !CheckStat(NearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(NearestEnemyOf(Myself),0,MINORGLOBE)
  !CheckStat(NearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  !CheckStat(NearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStat(NearestEnemyOf(Myself),1,POTION_OF_MAGIC_BLOCKING)
  !StateCheck(NearestEnemyOf(Myself),STATE_SLOWED)
  !StateCheck(NearestEnemyOf(Myself),STATE_SLEEPING)
  !StateCheck(NearestEnemyOf(Myself),STATE_STUNNED)
  !StateCheck(NearestEnemyOf(Myself),STATE_PANIC)
  !StateCheck(NearestEnemyOf(Myself),STATE_FEEBLEMINDED)
  !StateCheck(NearestEnemyOf(Myself),STATE_BLIND)
  !CheckStatGT(NearestEnemyOf(Myself),0,HELD)
  !StateCheck(NearestEnemyOf(Myself),STATE_HELPLESS)
  !StateCheck(NearestEnemyOf(Myself),STATE_CONFUSED)
  !StateCheck(NearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(NearestEnemyOf(Myself),0,SANCTUARY)
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !Race(SixthNearestEnemyOf(Myself),ELF)
  !CheckStat(SixthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(SixthNearestEnemyOf(Myself),0,MINORGLOBE)
  !CheckStat(SixthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  !CheckStat(SixthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStat(SixthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_BLOCKING)
  !StateCheck(SixthNearestEnemyOf(Myself),STATE_SLOWED)
  !StateCheck(SixthNearestEnemyOf(Myself),STATE_SLEEPING)
  !StateCheck(SixthNearestEnemyOf(Myself),STATE_STUNNED)
  !StateCheck(SixthNearestEnemyOf(Myself),STATE_PANIC)
  !StateCheck(SixthNearestEnemyOf(Myself),STATE_FEEBLEMINDED)
  !StateCheck(SixthNearestEnemyOf(Myself),STATE_BLIND)
  !CheckStatGT(SixthNearestEnemyOf(Myself),0,HELD)
  !StateCheck(SixthNearestEnemyOf(Myself),STATE_HELPLESS)
  !StateCheck(SixthNearestEnemyOf(Myself),STATE_CONFUSED)
  HPPercentGT(SixthNearestEnemyOf(Myself),30)
  !StateCheck(SixthNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(SixthNearestEnemyOf(Myself),0,SANCTUARY)
  OR(3)
    Class(SixthNearestEnemyOf(Myself),FIGHTER_ALL)
    Class(SixthNearestEnemyOf(Myself),PALADIN_ALL)
    Class(SixthNearestEnemyOf(Myself),RANGER_ALL)
  See(SixthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !Race(FifthNearestEnemyOf(Myself),ELF)
  !CheckStat(FifthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(FifthNearestEnemyOf(Myself),0,MINORGLOBE)
  !CheckStat(FifthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  !CheckStat(FifthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStat(FifthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_BLOCKING)
  !StateCheck(FifthNearestEnemyOf(Myself),STATE_SLOWED)
  !StateCheck(FifthNearestEnemyOf(Myself),STATE_SLEEPING)
  !StateCheck(FifthNearestEnemyOf(Myself),STATE_STUNNED)
  !StateCheck(FifthNearestEnemyOf(Myself),STATE_PANIC)
  !StateCheck(FifthNearestEnemyOf(Myself),STATE_FEEBLEMINDED)
  !StateCheck(FifthNearestEnemyOf(Myself),STATE_BLIND)
  !CheckStatGT(FifthNearestEnemyOf(Myself),0,HELD)
  !StateCheck(FifthNearestEnemyOf(Myself),STATE_HELPLESS)
  !StateCheck(FifthNearestEnemyOf(Myself),STATE_CONFUSED)
  HPPercentGT(FifthNearestEnemyOf(Myself),30)
  !StateCheck(FifthNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(FifthNearestEnemyOf(Myself),0,SANCTUARY)
  OR(3)
    Class(FifthNearestEnemyOf(Myself),FIGHTER_ALL)
    Class(FifthNearestEnemyOf(Myself),PALADIN_ALL)
    Class(FifthNearestEnemyOf(Myself),RANGER_ALL)
  See(FifthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !Race(FourthNearestEnemyOf(Myself),ELF)
  !CheckStat(FourthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(FourthNearestEnemyOf(Myself),0,MINORGLOBE)
  !CheckStat(FourthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  !CheckStat(FourthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStat(FourthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_BLOCKING)
  !StateCheck(FourthNearestEnemyOf(Myself),STATE_SLOWED)
  !StateCheck(FourthNearestEnemyOf(Myself),STATE_SLEEPING)
  !StateCheck(FourthNearestEnemyOf(Myself),STATE_STUNNED)
  !StateCheck(FourthNearestEnemyOf(Myself),STATE_PANIC)
  !StateCheck(FourthNearestEnemyOf(Myself),STATE_FEEBLEMINDED)
  !StateCheck(FourthNearestEnemyOf(Myself),STATE_BLIND)
  !CheckStatGT(FourthNearestEnemyOf(Myself),0,HELD)
  !StateCheck(FourthNearestEnemyOf(Myself),STATE_HELPLESS)
  !StateCheck(FourthNearestEnemyOf(Myself),STATE_CONFUSED)
  HPPercentGT(FourthNearestEnemyOf(Myself),30)
  !StateCheck(FourthNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(FourthNearestEnemyOf(Myself),0,SANCTUARY)
  OR(3)
    Class(FourthNearestEnemyOf(Myself),FIGHTER_ALL)
    Class(FourthNearestEnemyOf(Myself),PALADIN_ALL)
    Class(FourthNearestEnemyOf(Myself),RANGER_ALL)
  See(FourthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !Race(ThirdNearestEnemyOf(Myself),ELF)
  !CheckStat(ThirdNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(ThirdNearestEnemyOf(Myself),0,MINORGLOBE)
  !CheckStat(ThirdNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  !CheckStat(ThirdNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStat(ThirdNearestEnemyOf(Myself),1,POTION_OF_MAGIC_BLOCKING)
  !StateCheck(ThirdNearestEnemyOf(Myself),STATE_SLOWED)
  !StateCheck(ThirdNearestEnemyOf(Myself),STATE_SLEEPING)
  !StateCheck(ThirdNearestEnemyOf(Myself),STATE_STUNNED)
  !StateCheck(ThirdNearestEnemyOf(Myself),STATE_PANIC)
  !StateCheck(ThirdNearestEnemyOf(Myself),STATE_FEEBLEMINDED)
  !StateCheck(ThirdNearestEnemyOf(Myself),STATE_BLIND)
  !CheckStatGT(ThirdNearestEnemyOf(Myself),0,HELD)
  !StateCheck(ThirdNearestEnemyOf(Myself),STATE_HELPLESS)
  !StateCheck(ThirdNearestEnemyOf(Myself),STATE_CONFUSED)
  HPPercentGT(ThirdNearestEnemyOf(Myself),30)
  !StateCheck(ThirdNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(ThirdNearestEnemyOf(Myself),0,SANCTUARY)
  OR(3)
    Class(ThirdNearestEnemyOf(Myself),FIGHTER_ALL)
    Class(ThirdNearestEnemyOf(Myself),PALADIN_ALL)
    Class(ThirdNearestEnemyOf(Myself),RANGER_ALL)
  See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !Race(SecondNearestEnemyOf(Myself),ELF)
  !CheckStat(SecondNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(SecondNearestEnemyOf(Myself),0,MINORGLOBE)
  !CheckStat(SecondNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  !CheckStat(SecondNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStat(SecondNearestEnemyOf(Myself),1,POTION_OF_MAGIC_BLOCKING)
  !StateCheck(SecondNearestEnemyOf(Myself),STATE_SLOWED)
  !StateCheck(SecondNearestEnemyOf(Myself),STATE_SLEEPING)
  !StateCheck(SecondNearestEnemyOf(Myself),STATE_STUNNED)
  !StateCheck(SecondNearestEnemyOf(Myself),STATE_PANIC)
  !StateCheck(SecondNearestEnemyOf(Myself),STATE_FEEBLEMINDED)
  !StateCheck(SecondNearestEnemyOf(Myself),STATE_BLIND)
  !CheckStatGT(SecondNearestEnemyOf(Myself),0,HELD)
  !StateCheck(SecondNearestEnemyOf(Myself),STATE_HELPLESS)
  !StateCheck(SecondNearestEnemyOf(Myself),STATE_CONFUSED)
  HPPercentGT(SecondNearestEnemyOf(Myself),30)
  !StateCheck(SecondNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(SecondNearestEnemyOf(Myself),0,SANCTUARY)
  OR(3)
    Class(SecondNearestEnemyOf(Myself),FIGHTER_ALL)
    Class(SecondNearestEnemyOf(Myself),PALADIN_ALL)
    Class(SecondNearestEnemyOf(Myself),RANGER_ALL)
  See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !Race(NearestEnemyOf(Myself),ELF)
  !CheckStat(NearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(NearestEnemyOf(Myself),0,MINORGLOBE)
  !CheckStat(NearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  !CheckStat(NearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStat(NearestEnemyOf(Myself),1,POTION_OF_MAGIC_BLOCKING)
  !StateCheck(NearestEnemyOf(Myself),STATE_SLOWED)
  !StateCheck(NearestEnemyOf(Myself),STATE_SLEEPING)
  !StateCheck(NearestEnemyOf(Myself),STATE_STUNNED)
  !StateCheck(NearestEnemyOf(Myself),STATE_PANIC)
  !StateCheck(NearestEnemyOf(Myself),STATE_FEEBLEMINDED)
  !StateCheck(NearestEnemyOf(Myself),STATE_BLIND)
  !CheckStatGT(NearestEnemyOf(Myself),0,HELD)
  !StateCheck(NearestEnemyOf(Myself),STATE_HELPLESS)
  !StateCheck(NearestEnemyOf(Myself),STATE_CONFUSED)
  HPPercentGT(NearestEnemyOf(Myself),30)
  !StateCheck(NearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(NearestEnemyOf(Myself),0,SANCTUARY)
  OR(3)
    Class(NearestEnemyOf(Myself),FIGHTER_ALL)
    Class(NearestEnemyOf(Myself),PALADIN_ALL)
    Class(NearestEnemyOf(Myself),RANGER_ALL)
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Global("ja#disable_spellcasting","LOCALS",0)
  !GlobalTimerNotExpired("ja#round","LOCALS")
  HaveSpellRES("ja#SIRIN")
  !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  !CheckStat(LastSeenBy(Myself),1,POTION_OF_MAGIC_PROTECTION)
  !CheckStat(LastSeenBy(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStat(LastSeenBy(Myself),1,POTION_OF_MAGIC_BLOCKING)
  !StateCheck(LastSeenBy(Myself),STATE_SLOWED)
  !StateCheck(LastSeenBy(Myself),STATE_SLEEPING)
  !StateCheck(LastSeenBy(Myself),STATE_STUNNED)
  !StateCheck(LastSeenBy(Myself),STATE_PANIC)
  !StateCheck(LastSeenBy(Myself),STATE_FEEBLEMINDED)
  !StateCheck(LastSeenBy(Myself),STATE_BLIND)
  !CheckStatGT(LastSeenBy(Myself),0,HELD)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
  !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(LastSeenBy(Myself),0,SANCTUARY)
  See(LastSeenBy(Myself))  
THEN
  RESPONSE #100
    SetGlobalTimer("ja#round","LOCALS",6)
    SpellRES("ja#SIRIN",LastSeenBy(Myself))
END


/////////////////////////////////////////////////////////////////////
// Find a slept target. this only work for PC's enemies            //
// because sleeping creatures are not considered as enemies        //
/////////////////////////////////////////////////////////////////////
IF
  !StateCheck(Player6,STATE_CHARMED)
  CheckStat(Player6,0,SANCTUARY)
  StateCheck(Player6,STATE_SLEEPING)
  See(Player6)
  False()
THEN
  RESPONSE #100
END

IF
  !StateCheck(Player5,STATE_CHARMED)
  CheckStat(Player5,0,SANCTUARY)
  StateCheck(Player5,STATE_SLEEPING)
  See(Player5)
  False()
THEN
  RESPONSE #100
END

IF
  !StateCheck(Player4,STATE_CHARMED)
  CheckStat(Player4,0,SANCTUARY)
  StateCheck(Player4,STATE_SLEEPING)
  See(Player4)
  False()
THEN
  RESPONSE #100
END

IF
  !StateCheck(Player3,STATE_CHARMED)
  CheckStat(Player3,0,SANCTUARY)
  StateCheck(Player3,STATE_SLEEPING)
  See(Player3)
  False()
THEN
  RESPONSE #100
END

IF
  !StateCheck(Player2,STATE_CHARMED)
  CheckStat(Player2,0,SANCTUARY)
  StateCheck(Player2,STATE_SLEEPING)
  See(Player2)
  False()
THEN
  RESPONSE #100
END

IF
  !StateCheck(Player1,STATE_CHARMED)
  CheckStat(Player1,0,SANCTUARY)
  StateCheck(Player1,STATE_SLEEPING)
  See(Player1)
  False()
THEN
  RESPONSE #100
END


/////////////////////////////////////////////////////////////////////
// Safe attack, target is sleeping
/////////////////////////////////////////////////////////////////////
IF
  Allegiance(Myself,Enemy)
  !StateCheck(LastSeenBy(Myself),STATE_CHARMED)
  CheckStat(LastSeenBy(Myself),0,SANCTUARY)  
  StateCheck(LastSeenBy(Myself),STATE_SLEEPING)
  See(LastSeenBy(Myself))  
THEN
  RESPONSE #100
    //AttackReevaluate(LastSeenBy(Myself),15)
    AttackOneRound(LastSeenBy(Myself))
END


/////////////////////////////////////////////////////////////////////
// Find a helpless target to attack
/////////////////////////////////////////////////////////////////////
IF
  CheckStat(SixthNearestEnemyOf(Myself),0,SANCTUARY)
  OR(2)
    StateCheck(SixthNearestEnemyOf(Myself),STATE_STUNNED)
    CheckStatGT(SixthNearestEnemyOf(Myself),0,HELD)
  See(SixthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(FifthNearestEnemyOf(Myself),0,SANCTUARY)
  OR(2)
    StateCheck(FifthNearestEnemyOf(Myself),STATE_STUNNED)
    CheckStatGT(FifthNearestEnemyOf(Myself),0,HELD)
  See(FifthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(FourthNearestEnemyOf(Myself),0,SANCTUARY)
  OR(2)
    StateCheck(FourthNearestEnemyOf(Myself),STATE_STUNNED)
    CheckStatGT(FourthNearestEnemyOf(Myself),0,HELD)
  See(FourthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(ThirdNearestEnemyOf(Myself),0,SANCTUARY)
  OR(2)
    StateCheck(ThirdNearestEnemyOf(Myself),STATE_STUNNED)
    CheckStatGT(ThirdNearestEnemyOf(Myself),0,HELD)
  See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(SecondNearestEnemyOf(Myself),0,SANCTUARY)
  OR(2)
    StateCheck(SecondNearestEnemyOf(Myself),STATE_STUNNED)
    CheckStatGT(SecondNearestEnemyOf(Myself),0,HELD)
  See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(NearestEnemyOf(Myself),0,SANCTUARY)  
  OR(2)
    StateCheck(NearestEnemyOf(Myself),STATE_STUNNED)
    CheckStatGT(NearestEnemyOf(Myself),0,HELD)
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END


/////////////////////////////////////////////////////////////////////
// Safe attack, target can't escape
/////////////////////////////////////////////////////////////////////
IF
  CheckStat(LastSeenBy(Myself),0,SANCTUARY)  
  OR(2)
    StateCheck(LastSeenBy(Myself),STATE_STUNNED)
    CheckStatGT(LastSeenBy(Myself),0,HELD)
  See(LastSeenBy(Myself))  
THEN
  RESPONSE #100
    //AttackReevaluate(LastSeenBy(Myself),15)
    AttackOneRound(LastSeenBy(Myself))
END


/////////////////////////////////////////////////////////////////////
// Find something to attack, which has bonus on to hit
/////////////////////////////////////////////////////////////////////
IF
  CheckStat(SixthNearestEnemyOf(Myself),0,SANCTUARY)
  OR(3)
    StateCheck(SixthNearestEnemyOf(Myself),STATE_SLOWED)
    StateCheck(SixthNearestEnemyOf(Myself),STATE_BLIND)
    StateCheck(SixthNearestEnemyOf(Myself),STATE_HELPLESS)
  See(SixthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(FifthNearestEnemyOf(Myself),0,SANCTUARY)
  OR(3)
    StateCheck(FifthNearestEnemyOf(Myself),STATE_SLOWED)
    StateCheck(FifthNearestEnemyOf(Myself),STATE_BLIND)
    StateCheck(FifthNearestEnemyOf(Myself),STATE_HELPLESS)
  See(FifthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(FourthNearestEnemyOf(Myself),0,SANCTUARY)
  OR(3)
    StateCheck(FourthNearestEnemyOf(Myself),STATE_SLOWED)
    StateCheck(FourthNearestEnemyOf(Myself),STATE_BLIND)
    StateCheck(FourthNearestEnemyOf(Myself),STATE_HELPLESS)
  See(FourthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(ThirdNearestEnemyOf(Myself),0,SANCTUARY)
  OR(3)
    StateCheck(ThirdNearestEnemyOf(Myself),STATE_SLOWED)
    StateCheck(ThirdNearestEnemyOf(Myself),STATE_BLIND)
    StateCheck(ThirdNearestEnemyOf(Myself),STATE_HELPLESS)
  See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(SecondNearestEnemyOf(Myself),0,SANCTUARY)
  OR(3)
    StateCheck(SecondNearestEnemyOf(Myself),STATE_SLOWED)
    StateCheck(SecondNearestEnemyOf(Myself),STATE_BLIND)
    StateCheck(SecondNearestEnemyOf(Myself),STATE_HELPLESS)
  See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(NearestEnemyOf(Myself),0,SANCTUARY)  
  OR(3)
    StateCheck(NearestEnemyOf(Myself),STATE_SLOWED)
    StateCheck(NearestEnemyOf(Myself),STATE_BLIND)
    StateCheck(NearestEnemyOf(Myself),STATE_HELPLESS)
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END


/////////////////////////////////////////////////////////////////////
// target is easier to hit
/////////////////////////////////////////////////////////////////////
IF
  CheckStat(LastSeenBy(Myself),0,SANCTUARY)  
  OR(3)
    StateCheck(LastSeenBy(Myself),STATE_SLOWED)
    StateCheck(LastSeenBy(Myself),STATE_BLIND)
    StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
  See(LastSeenBy(Myself))  
THEN
  RESPONSE #100
    //AttackReevaluate(LastSeenBy(Myself),15)
    AttackOneRound(LastSeenBy(Myself))
END


/////////////////////////////////////////////////////////////////////
// Find something to attack, which hasn't control of itself
/////////////////////////////////////////////////////////////////////
IF
  CheckStat(SixthNearestEnemyOf(Myself),0,SANCTUARY)
  OR(3)
    StateCheck(SixthNearestEnemyOf(Myself),STATE_PANIC)
    StateCheck(SixthNearestEnemyOf(Myself),STATE_FEEBLEMINDED)
    StateCheck(SixthNearestEnemyOf(Myself),STATE_CONFUSED)
  See(SixthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(FifthNearestEnemyOf(Myself),0,SANCTUARY)
  OR(3)
    StateCheck(FifthNearestEnemyOf(Myself),STATE_PANIC)
    StateCheck(FifthNearestEnemyOf(Myself),STATE_FEEBLEMINDED)
    StateCheck(FifthNearestEnemyOf(Myself),STATE_CONFUSED)
  See(FifthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(FourthNearestEnemyOf(Myself),0,SANCTUARY)
  OR(3)
    StateCheck(FourthNearestEnemyOf(Myself),STATE_PANIC)
    StateCheck(FourthNearestEnemyOf(Myself),STATE_FEEBLEMINDED)
    StateCheck(FourthNearestEnemyOf(Myself),STATE_CONFUSED)
  See(FourthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(ThirdNearestEnemyOf(Myself),0,SANCTUARY)
  OR(3)
    StateCheck(ThirdNearestEnemyOf(Myself),STATE_PANIC)
    StateCheck(ThirdNearestEnemyOf(Myself),STATE_FEEBLEMINDED)
    StateCheck(ThirdNearestEnemyOf(Myself),STATE_CONFUSED)
  See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(SecondNearestEnemyOf(Myself),0,SANCTUARY)
  OR(3)
    StateCheck(SecondNearestEnemyOf(Myself),STATE_PANIC)
    StateCheck(SecondNearestEnemyOf(Myself),STATE_FEEBLEMINDED)
    StateCheck(SecondNearestEnemyOf(Myself),STATE_CONFUSED)
  See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(NearestEnemyOf(Myself),0,SANCTUARY)  
  OR(3)
    StateCheck(NearestEnemyOf(Myself),STATE_PANIC)
    StateCheck(NearestEnemyOf(Myself),STATE_FEEBLEMINDED)
    StateCheck(NearestEnemyOf(Myself),STATE_CONFUSED)
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END


/////////////////////////////////////////////////////////////////////
// Easy attack, target hasn't control of itself
/////////////////////////////////////////////////////////////////////
IF
  CheckStat(LastSeenBy(Myself),0,SANCTUARY)  
  OR(3)
    StateCheck(LastSeenBy(Myself),STATE_PANIC)
    StateCheck(LastSeenBy(Myself),STATE_FEEBLEMINDED)
    StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
THEN
  RESPONSE #100
    //AttackReevaluate(LastSeenBy(Myself),15)
    AttackOneRound(LastSeenBy(Myself))
END


/////////////////////////////////////////////////////////////////////
// Random attack (priority on melee range target)
/////////////////////////////////////////////////////////////////////

// three melee target
IF
  Range(ThirdNearestEnemyOf(Myself),8)
  RandomNum(6,1)
  OR(3)
    See(NearestEnemyOf(Myself))
    See(SecondNearestEnemyOf(Myself))
    See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Range(ThirdNearestEnemyOf(Myself),8)
  RandomNum(6,2)
  OR(3)
    See(SecondNearestEnemyOf(Myself))
    See(NearestEnemyOf(Myself))
    See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Range(ThirdNearestEnemyOf(Myself),8)
  RandomNum(6,3)
  OR(3)
    See(NearestEnemyOf(Myself))
    See(ThirdNearestEnemyOf(Myself))
    See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Range(ThirdNearestEnemyOf(Myself),8)
  RandomNum(6,4)
  OR(3)
    See(ThirdNearestEnemyOf(Myself))
    See(NearestEnemyOf(Myself))
    See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Range(ThirdNearestEnemyOf(Myself),8)
  RandomNum(6,5)
  OR(3)
    See(SecondNearestEnemyOf(Myself))
    See(ThirdNearestEnemyOf(Myself))
    See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Range(ThirdNearestEnemyOf(Myself),8)
  RandomNum(6,6)
  OR(3)
    See(ThirdNearestEnemyOf(Myself))
    See(SecondNearestEnemyOf(Myself))
    See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

// two melee target
IF
  !Range(ThirdNearestEnemyOf(Myself),8)
  Range(SecondNearestEnemyOf(Myself),8)
  RandomNum(2,1)
  OR(2)
    See(NearestEnemyOf(Myself))
    See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !Range(ThirdNearestEnemyOf(Myself),8)
  Range(SecondNearestEnemyOf(Myself),8)
  RandomNum(2,2)
  OR(2)
    See(SecondNearestEnemyOf(Myself))
    See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

// one melee target
IF
  !Range(SecondNearestEnemyOf(Myself),8)
  Range(NearestEnemyOf(Myself),8)
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Range(LastSeenBy(Myself),8)
  See(LastSeenBy(Myself))
  CheckStat(LastSeenBy(Myself),0,SANCTUARY)
  //!GlobalTimerNotExpired("ja#attack","LOCALS")
THEN
  RESPONSE #100
    //SetGlobalTimer("ja#attack","LOCALS",6)
    EquipMostDamagingMelee()
    AttackOneRound(LastSeenBy(Myself))
END


// No target in melee range, pick a random one
IF
  RandomNum(8,1)
  See(NearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  See(FourthNearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  See(SixthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  RandomNum(8,2)
  See(FourthNearestEnemyOf(Myself))
  See(NearestEnemyOf(Myself))
  See(SixthNearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  RandomNum(8,3)
  See(SixthNearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  See(NearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  See(FourthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  RandomNum(8,4)
  See(FourthNearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  See(SixthNearestEnemyOf(Myself))
  See(NearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  RandomNum(8,5)
  See(SixthNearestEnemyOf(Myself))
  See(FourthNearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  See(NearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  RandomNum(8,6)
  See(SixthNearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  See(FourthNearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  RandomNum(8,7)
  See(SixthNearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  See(FourthNearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  See(NearestEnemyOf(Myself))
  See(WeakestOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  RandomNum(8,8)
  See(SixthNearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  See(FourthNearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  See(NearestEnemyOf(Myself))
  See(MostDamagedOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !Range(LastSeenBy(Myself),5)
  CheckStat(LastSeenBy(Myself),0,SANCTUARY)
  //!GlobalTimerNotExpired("ja#attack","LOCALS")
THEN
  RESPONSE #100
    //SetGlobalTimer("ja#attack","LOCALS",6)
    EquipRanged()
    AttackOneRound(LastSeenBy(Myself))
END
