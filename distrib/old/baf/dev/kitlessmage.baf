///////////////////////////////////////////////////////////////////////
// Mage : kitless
// - contingency and chain contingencies
// - minor sequencer, spell sequencer, spell trigger
///////////////////////////////////////////////////////////////////////


/////////////////////////////////////////////////////////////////////
// Contingency, level 12-14
/////////////////////////////////////////////////////////////////////
IF
	Global("ja#contigency","LOCALS",0)
	CheckStatGT(Myself,11,LEVEL)
	CheckStatLT(Myself,15,LEVEL)
THEN
	RESPONSE #100
		SetGlobal("ja#contigency","LOCALS",1)
		DisplayString(Myself,84763) // Contingency Released
		ReallyForceSpell(Myself,WIZARD_MINOR_GLOBE_OF_INVULNERABILITY)
END


/////////////////////////////////////////////////////////////////////
// Contingency, level 15-17
/////////////////////////////////////////////////////////////////////
IF
	Global("ja#contigency","LOCALS",0)
	CheckStatGT(Myself,14,LEVEL)
	CheckStatLT(Myself,18,LEVEL)
THEN
	RESPONSE #100
		SetGlobal("ja#contigency","LOCALS",1)
		DisplayString(Myself,84763) // Contingency Released
		ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_ANIMATE_DEAD)
END


/////////////////////////////////////////////////////////////////////
// Chain contingencies, level 18-19
/////////////////////////////////////////////////////////////////////
IF
	Global("ja#contigency","LOCALS",0)
	CheckStatGT(Myself,17,LEVEL)
	CheckStatLT(Myself,20,LEVEL)
THEN
	RESPONSE #100
		SetGlobal("ja#contigency","LOCALS",1)
		DisplayString(Myself,26328) // Chain Contingency
		ReallyForceSpell(Myself,WIZARD_SPELL_TURNING)
		ReallyForceSpell(Myself,WIZARD_MORDENKAINENS_SWORD)		
		ReallyForceSpell(Myself,WIZARD_IMPROVED_MANTLE)
		SetGlobalTimer("ja#weaponprot","LOCALS",24)
END


/////////////////////////////////////////////////////////////////////
// Chain contingencies, level 20+
/////////////////////////////////////////////////////////////////////
IF
	Global("ja#contigency","LOCALS",0)
	CheckStatGT(Myself,19,LEVEL)
THEN
	RESPONSE #100
		SetGlobal("ja#contigency","LOCALS",1)
		DisplayString(Myself,26328) // Chain Contingency
		ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
		SmallWait(1)
		ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
		SmallWait(1)
		ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
END


/////////////////////////////////////////////////////////////////////
// Minor sequencer, level 7-13
/////////////////////////////////////////////////////////////////////
IF
	Global("ja#sequencer","LOCALS",0)
	!GlobalTimerNotExpired("ja#cast","LOCALS")
	Global("ja#disable_spellcasting","LOCALS",0)
	CheckStatGT(Myself,6,LEVEL)
	CheckStatLT(Myself,14,LEVEL)
THEN
	RESPONSE #100
		SetGlobalTimer("ja#cast","LOCALS",6)
		DisplayString(Myself,8635) // Minor Sequencer Activated
		ReallyForceSpell(Myself,WIZARD_SHIELD)
		ReallyForceSpell(Myself,WIZARD_MIRROR_IMAGE)
		SetGlobal("ja#sequencer","LOCALS",1)
END


/////////////////////////////////////////////////////////////////////
// Spell sequencer, level 14-15
/////////////////////////////////////////////////////////////////////
IF
	Global("ja#sequencer","LOCALS",0)
	!GlobalTimerNotExpired("ja#cast","LOCALS")
	Global("ja#disable_spellcasting","LOCALS",0)
	CheckStatGT(Myself,13,LEVEL)
	CheckStatLT(Myself,16,LEVEL)
THEN
	RESPONSE #100
		SetGlobalTimer("ja#cast","LOCALS",6)
		DisplayString(Myself,25951) // Spell Sequencer
		ReallyForceSpell(Myself,WIZARD_MINOR_GLOBE_OF_INVULNERABILITY)
		ReallyForceSpell(Myself,WIZARD_SPIRIT_ARMOR)
		ReallyForceSpell(Myself,WIZARD_FIRE_SHIELD_RED)
		SetGlobal("ja#sequencer","LOCALS",1)
END


/////////////////////////////////////////////////////////////////////
// Spell trigger, level 16+
/////////////////////////////////////////////////////////////////////
IF
	Global("ja#sequencer","LOCALS",0)
	!GlobalTimerNotExpired("ja#cast","LOCALS")
	Global("ja#disable_spellcasting","LOCALS",0)
	CheckStatGT(Myself,15,LEVEL)
THEN
	RESPONSE #100
		SetGlobalTimer("ja#cast","LOCALS",6)
		DisplayString(Myself,26243) // Spell Trigger
		ReallyForceSpell(Myself,WIZARD_TRUE_SIGHT)		
		ReallyForceSpell(Myself,WIZARD_GLOBE_OF_INVULNERABILITY)
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		SetGlobalTimer("ja#weaponprot","LOCALS",24)
		SetGlobal("ja#sequencer","LOCALS",1)
END


