// Dog (summon)

// Destroyed upon death
IF
  Die()
THEN
  RESPONSE #100
    DestroySelf()
END

IF
  Global("JA#COMBAT","LOCALS",0)
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    SetGlobal("JA#COMBAT","LOCALS",1)
    Shout(98)
END

IF
  Global("JA#COMBAT","LOCALS",0)
  Heard(LastSummonerOf(Myself),98)
  Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    SetGlobal("JA#COMBAT","LOCALS",1)
    Enemy()
END

// Follow summoner
IF
  Global("JA#COMBAT","LOCALS",0)
  !StateCheck(Myself,STATE_BLIND)
  ActionListEmpty()
  !See(NearestEnemyOf(Myself))
  !See(LastSummonerOf(Myself))
  !StateCheck(LastSummonerOf(Myself),STATE_INVISIBLE)
  HPGT(LastSummonerOf(Myself),0)
  Range(LastSummonerOf(Myself),70)
THEN
  RESPONSE #100
    MoveToObject(LastSummonerOf(Myself))
END

IF
  Global("JA#COMBAT","LOCALS",0)
  ActionListEmpty()
THEN
  RESPONSE #100
    NoAction()
END

// Track escaping target 
IF
  !StateCheck(Myself,STATE_BLIND)
  ActionListEmpty()
  !See(NearestEnemyOf(Myself))
  !CheckStatGT(LastAttackerOf(Myself),0,SANCTUARY)
  !StateCheck(LastAttackerOf(Myself),STATE_NOT_VISIBLE)
  HPGT(LastAttackerOf(Myself),0)
  Range(LastAttackerOf(Myself),100)
THEN
  RESPONSE #100
    MoveToObject(LastAttackerOf(Myself))
END

// Follow summoner
IF
  !StateCheck(Myself,STATE_BLIND)
  ActionListEmpty()
  !See(NearestEnemyOf(Myself))
  !See(LastSummonerOf(Myself))
  !StateCheck(LastSummonerOf(Myself),STATE_INVISIBLE)
  HPGT(LastSummonerOf(Myself),0)
  Range(LastSummonerOf(Myself),100)
THEN
  RESPONSE #100
    MoveToObject(LastSummonerOf(Myself))
END

// if no enemies seen, do nothing
IF
  ActionListEmpty()
  !See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    NoAction()
END

// Nearest target
IF
  ActionListEmpty()
  OR(3)
    General(NearestEnemyOf(Myself),WEAPON)
    CheckStatGT(NearestEnemyOf(Myself),0,SANCTUARY)
    !See(NearestEnemyOf(Myself))
  OR(3)
    General(SecondNearestEnemyOf(Myself),WEAPON)
    CheckStatGT(SecondNearestEnemyOf(Myself),0,SANCTUARY)
    !See(SecondNearestEnemyOf(Myself))
  OR(3)
    General(ThirdNearestEnemyOf(Myself),WEAPON)
    CheckStatGT(ThirdNearestEnemyOf(Myself),0,SANCTUARY)
    !See(ThirdNearestEnemyOf(Myself))
  OR(3)
    General(FourthNearestEnemyOf(Myself),WEAPON)
    CheckStatGT(FourthNearestEnemyOf(Myself),0,SANCTUARY)
    !See(FourthNearestEnemyOf(Myself))
  OR(3)
    General(FifthNearestEnemyOf(Myself),WEAPON)
    CheckStatGT(FifthNearestEnemyOf(Myself),0,SANCTUARY)
    !See(FifthNearestEnemyOf(Myself))
  OR(3)
    General(SixthNearestEnemyOf(Myself),WEAPON)
    CheckStatGT(SixthNearestEnemyOf(Myself),0,SANCTUARY)
    !See(SixthNearestEnemyOf(Myself))
THEN
  RESPONSE #100
    Continue()
END

IF
  !General(LastSeenBy(Myself),WEAPON)
  !CheckStatGT(LastSeenBy(Myself),0,SANCTUARY)
  See(LastSeenBy(Myself))
  ActionListEmpty()
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END

// Attack sleeping target
IF
  Allegiance(Myself,ENEMY)
  ActionListEmpty()
  OR(5)
    CheckStatGT(Player1,0,SANCTUARY)
    !StateCheck(Player1,STATE_SLEEPING)
    StateCheck(Player1,STATE_CHARMED)
    StateCheck(Player1,STATE_REALLY_DEAD)
    !See(Player1)
  OR(5)
    CheckStatGT(Player2,0,SANCTUARY)
    !StateCheck(Player2,STATE_SLEEPING)
    StateCheck(Player2,STATE_CHARMED)
    StateCheck(Player2,STATE_REALLY_DEAD)
    !See(Player2)
  OR(5)
    CheckStatGT(Player3,0,SANCTUARY)
    !StateCheck(Player3,STATE_SLEEPING)
    StateCheck(Player3,STATE_CHARMED)
    StateCheck(Player3,STATE_REALLY_DEAD)
    !See(Player3)
  OR(5)
    CheckStatGT(Player4,0,SANCTUARY)
    !StateCheck(Player4,STATE_SLEEPING)
    StateCheck(Player4,STATE_CHARMED)
    StateCheck(Player4,STATE_REALLY_DEAD)
    !See(Player4)
  OR(5)
    CheckStatGT(Player5,0,SANCTUARY)
    !StateCheck(Player5,STATE_SLEEPING)
    StateCheck(Player5,STATE_CHARMED)
    StateCheck(Player5,STATE_REALLY_DEAD)
    !See(Player5)
  OR(5)
    CheckStatGT(Player6,0,SANCTUARY)
    !StateCheck(Player6,STATE_SLEEPING)
    StateCheck(Player6,STATE_CHARMED)
    StateCheck(Player6,STATE_REALLY_DEAD)
    !See(Player6)
THEN
  RESPONSE #100
    Continue()
END

IF
  !General(LastSeenBy(Myself),WEAPON)
  !CheckStatGT(LastSeenBy(Myself),0,SANCTUARY)
  See(LastSeenBy(Myself))
  ActionListEmpty()
  Allegiance(Myself,ENEMY)
  StateCheck(LastSeenBy(Myself),STATE_SLEEPING)
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END

