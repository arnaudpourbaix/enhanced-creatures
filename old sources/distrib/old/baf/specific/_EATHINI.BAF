///////////////////////////////////////////////////////////////////////
// Demon Knight
///////////////////////////////////////////////////////////////////////
IF
  See([PC])
  NumTimesTalkedTo(0)
THEN
  RESPONSE #100
    SmallWait(2)
    StartDialog("_DEATH1",[PC])
END


/////////////////////////////////////////////////////////////////////
// Just after resting, everything is reset and creature is healed
/////////////////////////////////////////////////////////////////////
IF
  GlobalTimerExpired("ja#PrepTimer","LOCALS")
  Global("ja#initialize","LOCALS",1)
  !See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    Rest()
    ApplySpellRES("ja#HEAL",Myself)
    SetGlobal("ja#initialize","LOCALS",0)
END


///////////////////////////////////////////////////////////////////////
// Initialize variables & timers
///////////////////////////////////////////////////////////////////////
IF
  Global("ja#initialize","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("ja#initialize","LOCALS",1)
    SetGlobal("ja#combat","LOCALS",0)
    SetGlobal("DeathKnightDispel","LOCALS",0)
    SetGlobal("ja#powerwordkill","LOCALS",0)
    SetGlobal("ja#powerwordstun","LOCALS",0)
    SetGlobal("ja#symbolfear","LOCALS",0)
    SetGlobal("ja#fireball","LOCALS",0)
    SetGlobalTimer("ja#PrepTimer","LOCALS",2400)
END


/////////////////////////////////////////////////////////////////////
// See an enemy
/////////////////////////////////////////////////////////////////////
IF
  See(NearestEnemyOf(Myself))
  Global("ja#combat","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("ja#combat","LOCALS",1)
END


/////////////////////////////////////////////////////////////////////
// Do nothing if no enemy is detected and no combat started
/////////////////////////////////////////////////////////////////////
IF
  Global("ja#combat","LOCALS",0)
  !Detect(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    NoAction()
END


/////////////////////////////////////////////////////////////////////
// Are there any enemies that try to escape, follow them
// It's not about sight but memory (I saw that enemy going in that dir.)
/////////////////////////////////////////////////////////////////////
IF
  Allegiance(Myself,ENEMY)
  InMyArea(Player1)
  HPGT(Player1,0)
  !GlobalTimerNotExpired("ja#teleport","LOCALS")
  OR(2)
    !See(NearestEnemyOf(Myself))
    Global("ja#nothingtodo","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("ja#nothingtodo","LOCALS",0)
        SetGlobalTimer("ja#teleport","LOCALS",6)
    ForceSpellRES("SPWI931",PLAYER1)
END

IF
  Allegiance(Myself,ENEMY)
  InMyArea(Player2)
  HPGT(Player2,0)
  !GlobalTimerNotExpired("ja#teleport","LOCALS")
  OR(2)
    !See(NearestEnemyOf(Myself))
    Global("ja#nothingtodo","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("ja#nothingtodo","LOCALS",0)
        SetGlobalTimer("ja#teleport","LOCALS",6)
    ForceSpellRES("SPWI931",PLAYER2)
END

IF
  Allegiance(Myself,ENEMY)
  InMyArea(Player3)
  HPGT(Player3,0)
  !GlobalTimerNotExpired("ja#teleport","LOCALS")
  OR(2)
    !See(NearestEnemyOf(Myself))
    Global("ja#nothingtodo","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("ja#nothingtodo","LOCALS",0)
        SetGlobalTimer("ja#teleport","LOCALS",6)
    ForceSpellRES("SPWI931",PLAYER3)
END

IF
  Allegiance(Myself,ENEMY)
  InMyArea(Player4)
  HPGT(Player4,0)
  !GlobalTimerNotExpired("ja#teleport","LOCALS")
  OR(2)
    !See(NearestEnemyOf(Myself))
    Global("ja#nothingtodo","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("ja#nothingtodo","LOCALS",0)
        SetGlobalTimer("ja#teleport","LOCALS",6)
    ForceSpellRES("SPWI931",PLAYER4)
END

IF
  Allegiance(Myself,ENEMY)
  InMyArea(Player5)
  HPGT(Player5,0)
  !GlobalTimerNotExpired("ja#teleport","LOCALS")
  OR(2)
    !See(NearestEnemyOf(Myself))
    Global("ja#nothingtodo","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("ja#nothingtodo","LOCALS",0)
        SetGlobalTimer("ja#teleport","LOCALS",6)
    ForceSpellRES("SPWI931",PLAYER5)
END

IF
  Allegiance(Myself,ENEMY)
  InMyArea(Player6)
  HPGT(Player6,0)
  !GlobalTimerNotExpired("ja#teleport","LOCALS")
  OR(2)
    !See(NearestEnemyOf(Myself))
    Global("ja#nothingtodo","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("ja#nothingtodo","LOCALS",0)
        SetGlobalTimer("ja#teleport","LOCALS",6)
    ForceSpellRES("SPWI931",PLAYER6)
END


/////////////////////////////////////////////////////////////////////
// Do nothing if no enemy is detected
/////////////////////////////////////////////////////////////////////
IF
  !Detect(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    NoAction()
END


/////////////////////////////////////////////////////////////////////
// Demon fear
/////////////////////////////////////////////////////////////////////
IF
  !CheckStat(SixthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !StateCheck(SixthNearestEnemyOf(Myself),STATE_STUNNED)
  !StateCheck(SixthNearestEnemyOf(Myself),STATE_PANIC)
  CheckStat(SixthNearestEnemyOf(Myself),0,SANCTUARY)
  See(SixthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(FifthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !StateCheck(FifthNearestEnemyOf(Myself),STATE_STUNNED)
  !StateCheck(FifthNearestEnemyOf(Myself),STATE_PANIC)
  CheckStat(FifthNearestEnemyOf(Myself),0,SANCTUARY)
  See(FifthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(FourthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !StateCheck(FourthNearestEnemyOf(Myself),STATE_STUNNED)
  !StateCheck(FourthNearestEnemyOf(Myself),STATE_PANIC)
  CheckStat(FourthNearestEnemyOf(Myself),0,SANCTUARY)
  See(FourthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(ThirdNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !StateCheck(ThirdNearestEnemyOf(Myself),STATE_STUNNED)
  !StateCheck(ThirdNearestEnemyOf(Myself),STATE_PANIC)
  CheckStat(ThirdNearestEnemyOf(Myself),0,SANCTUARY)
  See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(SecondNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !StateCheck(SecondNearestEnemyOf(Myself),STATE_STUNNED)
  !StateCheck(SecondNearestEnemyOf(Myself),STATE_PANIC)
  CheckStat(SecondNearestEnemyOf(Myself),0,SANCTUARY)
  See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(NearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !StateCheck(NearestEnemyOf(Myself),STATE_STUNNED)
  !StateCheck(NearestEnemyOf(Myself),STATE_PANIC)
  CheckStat(NearestEnemyOf(Myself),0,SANCTUARY)
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  See(LastSeenBy(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_PANIC)
  Range(LastSeenBy(Myself),10)
  !GlobalTimerNotExpired("DemFear","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("DemFear","LOCALS",15)
    ForceSpell(LastSeenBy(Myself),DEMON_FEAR)
END


/////////////////////////////////////////////////////////////////////
// Dispel Magic
/////////////////////////////////////////////////////////////////////
IF
  !CheckStat(SixthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(SixthNearestEnemyOf(Myself),0,SANCTUARY)
  OR(5)
    CheckStatGT(SixthNearestEnemyOf(Myself),18,STR)
    HasImmunityEffects(SixthNearestEnemyOf(Myself))
    CheckStat(SixthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
    CheckStat(SixthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_BLOCKING)
    CheckStat(SixthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  See(SixthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(FifthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(FifthNearestEnemyOf(Myself),0,SANCTUARY)
  OR(5)
    CheckStatGT(FifthNearestEnemyOf(Myself),18,STR)
    HasImmunityEffects(FifthNearestEnemyOf(Myself))
    CheckStat(FifthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
    CheckStat(FifthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_BLOCKING)
    CheckStat(FifthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  See(FifthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(FourthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(FourthNearestEnemyOf(Myself),0,SANCTUARY)
  OR(5)  
    CheckStatGT(FourthNearestEnemyOf(Myself),18,STR)
    HasImmunityEffects(FourthNearestEnemyOf(Myself))
    CheckStat(FourthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
    CheckStat(FourthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_BLOCKING)
    CheckStat(FourthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  See(FourthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(ThirdNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(ThirdNearestEnemyOf(Myself),0,SANCTUARY)
  OR(5)
    CheckStatGT(ThirdNearestEnemyOf(Myself),18,STR)  
    HasImmunityEffects(ThirdNearestEnemyOf(Myself))  
    CheckStat(ThirdNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
    CheckStat(ThirdNearestEnemyOf(Myself),1,POTION_OF_MAGIC_BLOCKING)
    CheckStat(ThirdNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(SecondNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(SecondNearestEnemyOf(Myself),0,SANCTUARY)
  OR(5)
    CheckStatGT(SecondNearestEnemyOf(Myself),18,STR)
    HasImmunityEffects(SecondNearestEnemyOf(Myself))
    CheckStat(SecondNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
    CheckStat(SecondNearestEnemyOf(Myself),1,POTION_OF_MAGIC_BLOCKING)
    CheckStat(SecondNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(NearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(NearestEnemyOf(Myself),0,SANCTUARY)
  OR(5)  
    CheckStatGT(NearestEnemyOf(Myself),18,STR)
    HasImmunityEffects(NearestEnemyOf(Myself))
    CheckStat(NearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
    CheckStat(NearestEnemyOf(Myself),1,POTION_OF_MAGIC_BLOCKING)
    CheckStat(NearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !Global("DeathKnightDispel","LOCALS",2)
  !GlobalTimerNotExpired("ja#round","LOCALS")
  !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(LastSeenBy(Myself),0,SANCTUARY)
  OR(5)  
    CheckStatGT(LastSeenBy(Myself),18,STR)
    HasImmunityEffects(LastSeenBy(Myself))
    CheckStat(LastSeenBy(Myself),1,POTION_OF_MAGIC_SHIELDING)
    CheckStat(LastSeenBy(Myself),1,POTION_OF_MAGIC_BLOCKING)
    CheckStat(LastSeenBy(Myself),1,POTION_OF_MAGIC_PROTECTION)
  See(LastSeenBy(Myself))
THEN
  RESPONSE #100
      SetGlobalTimer("ja#round","LOCALS",6)
       ForceSpell(LastSeenBy(Myself),WIZARD_TRUE_DISPEL_MAGIC)
       IncrementGlobal("DeathKnightDispel","LOCALS",1)
END


/////////////////////////////////////////////////////////////////////
// Power word kill
/////////////////////////////////////////////////////////////////////
IF
  !CheckStat(SixthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(SixthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  CheckStat(SixthNearestEnemyOf(Myself),0,SANCTUARY)
  HPLT(SixthNearestEnemyOf(Myself),60)
  See(SixthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(FifthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3)
  !CheckStat(FifthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  CheckStat(FifthNearestEnemyOf(Myself),0,SANCTUARY)
  HPLT(FifthNearestEnemyOf(Myself),60)
  See(FifthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(FourthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3)
  !CheckStat(FourthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  CheckStat(FourthNearestEnemyOf(Myself),0,SANCTUARY)
  HPLT(FourthNearestEnemyOf(Myself),60)
  See(FourthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(ThirdNearestEnemyOf(Myself),1,SCRIPTINGSTATE3)
  !CheckStat(ThirdNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  CheckStat(ThirdNearestEnemyOf(Myself),0,SANCTUARY)
  HPLT(ThirdNearestEnemyOf(Myself),60)
  See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(SecondNearestEnemyOf(Myself),1,SCRIPTINGSTATE3)
  !CheckStat(SecondNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  CheckStat(SecondNearestEnemyOf(Myself),0,SANCTUARY)
  HPLT(SecondNearestEnemyOf(Myself),60)
  See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(NearestEnemyOf(Myself),1,SCRIPTINGSTATE3)
  !CheckStat(NearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  CheckStat(NearestEnemyOf(Myself),0,SANCTUARY)
  HPLT(NearestEnemyOf(Myself),60)
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Global("ja#powerwordkill","LOCALS",0)
  !GlobalTimerNotExpired("ja#round","LOCALS")
  !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE3)
  !CheckStat(LastSeenBy(Myself),1,POTION_OF_MAGIC_PROTECTION)
  CheckStat(LastSeenBy(Myself),0,SANCTUARY)
  HPLT(LastSeenBy(Myself),60)
  See(LastSeenBy(Myself))
THEN
  RESPONSE #100
    SetGlobalTimer("ja#round","LOCALS",6)
    SetGlobal("ja#powerwordkill","LOCALS",1)
    ForceSpell(LastSeenBy(Myself),WIZARD_POWER_WORD_KILL)
END


/////////////////////////////////////////////////////////////////////
// Power word stun
/////////////////////////////////////////////////////////////////////
IF
  !CheckStat(SixthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(SixthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  HPLT(SixthNearestEnemyOf(Myself),60)
  !StateCheck(SixthNearestEnemyOf(Myself),STATE_STUNNED)
  !StateCheck(SixthNearestEnemyOf(Myself),STATE_PANIC)
  CheckStat(SixthNearestEnemyOf(Myself),0,SANCTUARY)
  See(SixthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(FifthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(FifthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  HPLT(FifthNearestEnemyOf(Myself),60)
  !StateCheck(FifthNearestEnemyOf(Myself),STATE_STUNNED)
  !StateCheck(FifthNearestEnemyOf(Myself),STATE_PANIC)
  CheckStat(FifthNearestEnemyOf(Myself),0,SANCTUARY)
  See(FifthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(FourthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(FourthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  HPLT(FourthNearestEnemyOf(Myself),60)
  !StateCheck(FourthNearestEnemyOf(Myself),STATE_STUNNED)
  !StateCheck(FourthNearestEnemyOf(Myself),STATE_PANIC)
  CheckStat(FourthNearestEnemyOf(Myself),0,SANCTUARY)
  See(FourthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(ThirdNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(ThirdNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  HPLT(ThirdNearestEnemyOf(Myself),60)
  !StateCheck(ThirdNearestEnemyOf(Myself),STATE_STUNNED)
  !StateCheck(ThirdNearestEnemyOf(Myself),STATE_PANIC)
  CheckStat(ThirdNearestEnemyOf(Myself),0,SANCTUARY)
  See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(SecondNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(SecondNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  HPLT(SecondNearestEnemyOf(Myself),60)
  !StateCheck(SecondNearestEnemyOf(Myself),STATE_STUNNED)
  !StateCheck(SecondNearestEnemyOf(Myself),STATE_PANIC)
  CheckStat(SecondNearestEnemyOf(Myself),0,SANCTUARY)
  See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(NearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(NearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  HPLT(NearestEnemyOf(Myself),60)
  !StateCheck(NearestEnemyOf(Myself),STATE_STUNNED)
  !StateCheck(NearestEnemyOf(Myself),STATE_PANIC)
  CheckStat(NearestEnemyOf(Myself),0,SANCTUARY)
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Global("ja#powerwordstun","LOCALS",0)
  !GlobalTimerNotExpired("ja#round","LOCALS")
  !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(LastSeenBy(Myself),1,POTION_OF_MAGIC_PROTECTION)
  HPLT(LastSeenBy(Myself),60)
  !StateCheck(LastSeenBy(Myself),STATE_STUNNED)
  !StateCheck(LastSeenBy(Myself),STATE_PANIC)
  CheckStat(LastSeenBy(Myself),0,SANCTUARY)
  See(LastSeenBy(Myself))
THEN
  RESPONSE #100
    SetGlobalTimer("ja#round","LOCALS",6)
    SetGlobal("ja#powerwordstun","LOCALS",1)
    ForceSpell(LastSeenBy(Myself),WIZARD_POWER_WORD_STUN)
END


/////////////////////////////////////////////////////////////////////
// Symbol fear
/////////////////////////////////////////////////////////////////////
IF
  !CheckStat(SixthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(SixthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStat(SixthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  !StateCheck(SixthNearestEnemyOf(Myself),STATE_STUNNED)
  !StateCheck(SixthNearestEnemyOf(Myself),STATE_PANIC)
  CheckStat(SixthNearestEnemyOf(Myself),0,SANCTUARY)
  See(SixthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(FifthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(FifthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStat(FifthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  !StateCheck(FifthNearestEnemyOf(Myself),STATE_STUNNED)
  !StateCheck(FifthNearestEnemyOf(Myself),STATE_PANIC)
  CheckStat(FifthNearestEnemyOf(Myself),0,SANCTUARY)
  See(FifthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(FourthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(FourthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStat(FourthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  !StateCheck(FourthNearestEnemyOf(Myself),STATE_STUNNED)
  !StateCheck(FourthNearestEnemyOf(Myself),STATE_PANIC)
  CheckStat(FourthNearestEnemyOf(Myself),0,SANCTUARY)
  See(FourthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(ThirdNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(ThirdNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStat(ThirdNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  !StateCheck(ThirdNearestEnemyOf(Myself),STATE_STUNNED)
  !StateCheck(ThirdNearestEnemyOf(Myself),STATE_PANIC)
  CheckStat(ThirdNearestEnemyOf(Myself),0,SANCTUARY)
  See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(SecondNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(SecondNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStat(SecondNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  !StateCheck(SecondNearestEnemyOf(Myself),STATE_STUNNED)
  !StateCheck(SecondNearestEnemyOf(Myself),STATE_PANIC)
  CheckStat(SecondNearestEnemyOf(Myself),0,SANCTUARY)
  See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(NearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(NearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStat(NearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  !StateCheck(NearestEnemyOf(Myself),STATE_STUNNED)
  !StateCheck(NearestEnemyOf(Myself),STATE_PANIC)
  CheckStat(NearestEnemyOf(Myself),0,SANCTUARY)
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Global("ja#symbolfear","LOCALS",0)
  !GlobalTimerNotExpired("ja#round","LOCALS")
  !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(LastSeenBy(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStat(LastSeenBy(Myself),1,POTION_OF_MAGIC_PROTECTION)
  !StateCheck(LastSeenBy(Myself),STATE_STUNNED)
  !StateCheck(LastSeenBy(Myself),STATE_PANIC)
  CheckStat(LastSeenBy(Myself),0,SANCTUARY)
  See(LastSeenBy(Myself))
THEN
  RESPONSE #100
    SetGlobalTimer("ja#round","LOCALS",6)
    SetGlobal("ja#symbolfear","LOCALS",1)
    ForceSpell(LastSeenBy(Myself),WIZARD_SYMBOL_FEAR)
END


/////////////////////////////////////////////////////////////////////
// Fireball
/////////////////////////////////////////////////////////////////////
IF
  !CheckStat(SixthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(SixthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  !CheckStat(SixthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStat(SixthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_BLOCKING)
  CheckStat(SixthNearestEnemyOf(Myself),0,MINORGLOBE)
  CheckStat(SixthNearestEnemyOf(Myself),0,SANCTUARY)
  See(SixthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(FifthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(FifthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  !CheckStat(FifthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStat(FifthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_BLOCKING)
  CheckStat(FifthNearestEnemyOf(Myself),0,MINORGLOBE)
  CheckStat(FifthNearestEnemyOf(Myself),0,SANCTUARY)
  See(FifthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(FourthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(FourthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  !CheckStat(FourthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStat(FourthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_BLOCKING)
  CheckStat(FourthNearestEnemyOf(Myself),0,MINORGLOBE)
  CheckStat(FourthNearestEnemyOf(Myself),0,SANCTUARY)
  See(FourthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(ThirdNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(ThirdNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  !CheckStat(ThirdNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStat(ThirdNearestEnemyOf(Myself),1,POTION_OF_MAGIC_BLOCKING)
  CheckStat(ThirdNearestEnemyOf(Myself),0,MINORGLOBE)
  CheckStat(ThirdNearestEnemyOf(Myself),0,SANCTUARY)
  See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(SecondNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(SecondNearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  !CheckStat(SecondNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStat(SecondNearestEnemyOf(Myself),1,POTION_OF_MAGIC_BLOCKING)
  CheckStat(SecondNearestEnemyOf(Myself),0,MINORGLOBE)
  CheckStat(SecondNearestEnemyOf(Myself),0,SANCTUARY)
  See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(NearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(NearestEnemyOf(Myself),1,POTION_OF_MAGIC_PROTECTION)
  !CheckStat(NearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStat(NearestEnemyOf(Myself),1,POTION_OF_MAGIC_BLOCKING)
  CheckStat(NearestEnemyOf(Myself),0,MINORGLOBE)
  CheckStat(NearestEnemyOf(Myself),0,SANCTUARY)
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !GlobalTimerNotExpired("ja#round","LOCALS")
  Global("ja#fireball","LOCALS",0)
  !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(LastSeenBy(Myself),1,POTION_OF_MAGIC_PROTECTION)
  !CheckStat(LastSeenBy(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStat(LastSeenBy(Myself),1,POTION_OF_MAGIC_BLOCKING)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  CheckStat(LastSeenBy(Myself),0,SANCTUARY)
  See(LastSeenBy(Myself))
THEN
  RESPONSE #100
    SetGlobalTimer("ja#round","LOCALS",6)
    SetGlobal("ja#fireball","LOCALS",1)
    ForceSpell(LastSeenBy(Myself),DEATHKNIGHT_FIREBALL)
END


/////////////////////////////////////////////////////////////////////
// Find a helpless target to attack
/////////////////////////////////////////////////////////////////////
IF
  CheckStat(SixthNearestEnemyOf(Myself),0,SANCTUARY)
  StateCheck(SixthNearestEnemyOf(Myself),STATE_STUNNED)
  See(SixthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(FifthNearestEnemyOf(Myself),0,SANCTUARY)
  StateCheck(FifthNearestEnemyOf(Myself),STATE_STUNNED)
  See(FifthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(FourthNearestEnemyOf(Myself),0,SANCTUARY)
  StateCheck(FourthNearestEnemyOf(Myself),STATE_STUNNED)
  See(FourthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(ThirdNearestEnemyOf(Myself),0,SANCTUARY)
  StateCheck(ThirdNearestEnemyOf(Myself),STATE_STUNNED)
  See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(SecondNearestEnemyOf(Myself),0,SANCTUARY)
  StateCheck(SecondNearestEnemyOf(Myself),STATE_STUNNED)
  See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(NearestEnemyOf(Myself),0,SANCTUARY)  
  StateCheck(NearestEnemyOf(Myself),STATE_STUNNED)
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(LastSeenBy(Myself),0,SANCTUARY)  
  StateCheck(LastSeenBy(Myself),STATE_STUNNED)
  See(LastSeenBy(Myself))  
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END


/////////////////////////////////////////////////////////////////////
// Avoid non dangerous target
/////////////////////////////////////////////////////////////////////
IF
  CheckStat(SixthNearestEnemyOf(Myself),0,SANCTUARY)
  !StateCheck(SixthNearestEnemyOf(Myself),STATE_PANIC)
  See(SixthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(FifthNearestEnemyOf(Myself),0,SANCTUARY)
  !StateCheck(FifthNearestEnemyOf(Myself),STATE_PANIC)
  See(FifthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(FourthNearestEnemyOf(Myself),0,SANCTUARY)
  !StateCheck(FourthNearestEnemyOf(Myself),STATE_PANIC)
  See(FourthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(ThirdNearestEnemyOf(Myself),0,SANCTUARY)
  !StateCheck(ThirdNearestEnemyOf(Myself),STATE_PANIC)
  See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(SecondNearestEnemyOf(Myself),0,SANCTUARY)
  !StateCheck(SecondNearestEnemyOf(Myself),STATE_PANIC)
  See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(NearestEnemyOf(Myself),0,SANCTUARY)  
  !StateCheck(NearestEnemyOf(Myself),STATE_PANIC)
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(LastSeenBy(Myself),0,SANCTUARY)  
  !StateCheck(LastSeenBy(Myself),STATE_PANIC)
  See(LastSeenBy(Myself))  
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END


/////////////////////////////////////////////////////////////////////
// Random attack (priority on melee range target)
/////////////////////////////////////////////////////////////////////

// three melee target
IF
  Range(ThirdNearestEnemyOf(Myself),8)
  RandomNum(6,1)
  OR(3)
    See(NearestEnemyOf(Myself))
    See(SecondNearestEnemyOf(Myself))
    See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Range(ThirdNearestEnemyOf(Myself),8)
  RandomNum(6,2)
  OR(3)
    See(SecondNearestEnemyOf(Myself))
    See(NearestEnemyOf(Myself))
    See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Range(ThirdNearestEnemyOf(Myself),8)
  RandomNum(6,3)
  OR(3)
    See(NearestEnemyOf(Myself))
    See(ThirdNearestEnemyOf(Myself))
    See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Range(ThirdNearestEnemyOf(Myself),8)
  RandomNum(6,4)
  OR(3)
    See(ThirdNearestEnemyOf(Myself))
    See(NearestEnemyOf(Myself))
    See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Range(ThirdNearestEnemyOf(Myself),8)
  RandomNum(6,5)
  OR(3)
    See(SecondNearestEnemyOf(Myself))
    See(ThirdNearestEnemyOf(Myself))
    See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Range(ThirdNearestEnemyOf(Myself),8)
  RandomNum(6,6)
  OR(3)
    See(ThirdNearestEnemyOf(Myself))
    See(SecondNearestEnemyOf(Myself))
    See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

// two melee target
IF
  !Range(ThirdNearestEnemyOf(Myself),8)
  Range(SecondNearestEnemyOf(Myself),8)
  RandomNum(2,1)
  OR(2)
    See(NearestEnemyOf(Myself))
    See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !Range(ThirdNearestEnemyOf(Myself),8)
  Range(SecondNearestEnemyOf(Myself),8)
  RandomNum(2,2)
  OR(2)
    See(SecondNearestEnemyOf(Myself))
    See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

// one melee target
IF
  !Range(SecondNearestEnemyOf(Myself),8)
  Range(NearestEnemyOf(Myself),8)
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Range(LastSeenBy(Myself),8)
  See(LastSeenBy(Myself))
  CheckStat(LastSeenBy(Myself),0,SANCTUARY)
  //!GlobalTimerNotExpired("ja#attack","LOCALS")
THEN
  RESPONSE #100
    //SetGlobalTimer("ja#attack","LOCALS",6)
    AttackOneRound(LastSeenBy(Myself))
END


// No target in melee range, pick a random one
IF
  RandomNum(8,1)
  See(NearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  See(FourthNearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  See(SixthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  RandomNum(8,2)
  See(FourthNearestEnemyOf(Myself))
  See(NearestEnemyOf(Myself))
  See(SixthNearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  RandomNum(8,3)
  See(SixthNearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  See(NearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  See(FourthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  RandomNum(8,4)
  See(FourthNearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  See(SixthNearestEnemyOf(Myself))
  See(NearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  RandomNum(8,5)
  See(SixthNearestEnemyOf(Myself))
  See(FourthNearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  See(NearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  RandomNum(8,6)
  See(SixthNearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  See(FourthNearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  RandomNum(8,7)
  See(SixthNearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  See(FourthNearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  See(NearestEnemyOf(Myself))
  See(WeakestOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  RandomNum(8,8)
  See(SixthNearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  See(FourthNearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  See(NearestEnemyOf(Myself))
  See(MostDamagedOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !Range(LastSeenBy(Myself),5)
  CheckStat(LastSeenBy(Myself),0,SANCTUARY)
  //!GlobalTimerNotExpired("ja#attack","LOCALS")
THEN
  RESPONSE #100
    //SetGlobalTimer("ja#attack","LOCALS",6)
    AttackOneRound(LastSeenBy(Myself))
END


/////////////////////////////////////////////////////////////////////
// Nothing to do, teleport to someone
/////////////////////////////////////////////////////////////////////
IF
  ActionListEmpty()
  Global("ja#nothingtodo","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("ja#nothingtodo","LOCALS",1)
END