///////////////////////////////////////////////////////////////////////
// Greater Nabassu
///////////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////////
// Just after resting, everything is reset and creature is healed
/////////////////////////////////////////////////////////////////////
IF
  GlobalTimerExpired("ja#PrepTimer","LOCALS")
  Global("ja#initialize","LOCALS",1)
  !See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    Rest()
    ApplySpellRES("ja#HEAL",Myself)
    SetGlobal("ja#initialize","LOCALS",0)
END


///////////////////////////////////////////////////////////////////////
// Initialize variables & timers
///////////////////////////////////////////////////////////////////////
IF
  Global("ja#initialize","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("ja#initialize","LOCALS",1)
    SetGlobal("ja#combat","LOCALS",0)
    SetGlobal("ja#deathgaze","LOCALS",0) // 20 death gazes per day
    SetGlobal("ja#nothingtodo","LOCALS",0)
    SetGlobalTimer("ja#round","LOCALS",0)
    SetGlobalTimer("ja#PrepTimer","LOCALS",2400)
END


/////////////////////////////////////////////////////////////////////
// See an enemy
/////////////////////////////////////////////////////////////////////
IF
  See(NearestEnemyOf(Myself))
  Global("ja#combat","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("ja#combat","LOCALS",1)
END


/////////////////////////////////////////////////////////////////////
// Do nothing if no enemy is detected and no combat started
/////////////////////////////////////////////////////////////////////
IF
  Global("ja#combat","LOCALS",0)
  !Detect(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    NoAction()
END


/////////////////////////////////////////////////////////////////////
// Are there any enemies that try to escape, follow them
// It's not about sight but memory (I saw that enemy going in that dir.)
/////////////////////////////////////////////////////////////////////
IF
  Allegiance(Myself,ENEMY)
  InMyArea(Player1)
  HPGT(Player1,0)
  !GlobalTimerNotExpired("ja#teleport","LOCALS")
  OR(2)
    !See(NearestEnemyOf(Myself))
    Global("ja#nothingtodo","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("ja#nothingtodo","LOCALS",0)
        SetGlobalTimer("ja#teleport","LOCALS",6)
    ForceSpellRES("SPWI931",PLAYER1)
END

IF
  Allegiance(Myself,ENEMY)
  InMyArea(Player2)
  HPGT(Player2,0)
  !GlobalTimerNotExpired("ja#teleport","LOCALS")
  OR(2)
    !See(NearestEnemyOf(Myself))
    Global("ja#nothingtodo","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("ja#nothingtodo","LOCALS",0)
        SetGlobalTimer("ja#teleport","LOCALS",6)
    ForceSpellRES("SPWI931",PLAYER2)
END

IF
  Allegiance(Myself,ENEMY)
  InMyArea(Player3)
  HPGT(Player3,0)
  !GlobalTimerNotExpired("ja#teleport","LOCALS")
  OR(2)
    !See(NearestEnemyOf(Myself))
    Global("ja#nothingtodo","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("ja#nothingtodo","LOCALS",0)
        SetGlobalTimer("ja#teleport","LOCALS",6)
    ForceSpellRES("SPWI931",PLAYER3)
END

IF
  Allegiance(Myself,ENEMY)
  InMyArea(Player4)
  HPGT(Player4,0)
  !GlobalTimerNotExpired("ja#teleport","LOCALS")
  OR(2)
    !See(NearestEnemyOf(Myself))
    Global("ja#nothingtodo","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("ja#nothingtodo","LOCALS",0)
        SetGlobalTimer("ja#teleport","LOCALS",6)
    ForceSpellRES("SPWI931",PLAYER4)
END

IF
  Allegiance(Myself,ENEMY)
  InMyArea(Player5)
  HPGT(Player5,0)
  !GlobalTimerNotExpired("ja#teleport","LOCALS")
  OR(2)
    !See(NearestEnemyOf(Myself))
    Global("ja#nothingtodo","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("ja#nothingtodo","LOCALS",0)
        SetGlobalTimer("ja#teleport","LOCALS",6)
    ForceSpellRES("SPWI931",PLAYER5)
END

IF
  Allegiance(Myself,ENEMY)
  InMyArea(Player6)
  HPGT(Player6,0)
  !GlobalTimerNotExpired("ja#teleport","LOCALS")
  OR(2)
    !See(NearestEnemyOf(Myself))
    Global("ja#nothingtodo","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("ja#nothingtodo","LOCALS",0)
        SetGlobalTimer("ja#teleport","LOCALS",6)
    ForceSpellRES("SPWI931",PLAYER6)
END


/////////////////////////////////////////////////////////////////////
// Do nothing if no enemy is detected
/////////////////////////////////////////////////////////////////////
IF
  !Detect(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    NoAction()
END


/////////////////////////////////////////////////////////////////////
// Death gaze
/////////////////////////////////////////////////////////////////////
IF
  !CheckStat(SixthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(SixthNearestEnemyOf(Myself),0,SANCTUARY)
  See(SixthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(FifthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(FifthNearestEnemyOf(Myself),0,SANCTUARY)
  See(FifthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(FourthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(FourthNearestEnemyOf(Myself),0,SANCTUARY)
  See(FourthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(ThirdNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(ThirdNearestEnemyOf(Myself),0,SANCTUARY)
  See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(SecondNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(SecondNearestEnemyOf(Myself),0,SANCTUARY)
  See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(NearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(NearestEnemyOf(Myself),0,SANCTUARY)
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !Global("ja#deathgaze","LOCALS",20)
  !GlobalTimerNotExpired("ja#gaze","LOCALS")
  !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(LastSeenBy(Myself),0,SANCTUARY)
  See(LastSeenBy(Myself))
THEN
  RESPONSE #100
    SetGlobalTimer("ja#gaze","LOCALS",6)
    IncrementGlobal("ja#deathgaze","LOCALS",1)    
    ForceSpellRES("JA#tana1",LastSeenBy(Myself))
END


/////////////////////////////////////////////////////////////////////
// Silence 15' radius
/////////////////////////////////////////////////////////////////////
IF
  !CheckStat(SixthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(SixthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStatGT(SixthNearestEnemyOf(Myself),0,HELD)
  !StateCheck(SixthNearestEnemyOf(Myself),STATE_SILENCED)
  CheckStat(SixthNearestEnemyOf(Myself),0,SANCTUARY)
  OR(4)
    Class(SixthNearestEnemyOf(Myself),DRUID_ALL)
    Class(SixthNearestEnemyOf(Myself),BARD_ALL)
    Class(SixthNearestEnemyOf(Myself),CLERIC_ALL)
    Class(SixthNearestEnemyOf(Myself),MAGE_ALL)
  See(SixthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(FifthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(FifthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStatGT(FifthNearestEnemyOf(Myself),0,HELD)
  !StateCheck(FifthNearestEnemyOf(Myself),STATE_SILENCED)
  CheckStat(FifthNearestEnemyOf(Myself),0,SANCTUARY)
  OR(4)
    Class(FifthNearestEnemyOf(Myself),DRUID_ALL)
    Class(FifthNearestEnemyOf(Myself),BARD_ALL)
    Class(FifthNearestEnemyOf(Myself),CLERIC_ALL)
    Class(FifthNearestEnemyOf(Myself),MAGE_ALL)
  See(FifthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(FourthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(FourthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStatGT(FourthNearestEnemyOf(Myself),0,HELD)
  !StateCheck(FourthNearestEnemyOf(Myself),STATE_SILENCED)
  CheckStat(FourthNearestEnemyOf(Myself),0,SANCTUARY)
  OR(4)
    Class(FourthNearestEnemyOf(Myself),DRUID_ALL)
    Class(FourthNearestEnemyOf(Myself),BARD_ALL)
    Class(FourthNearestEnemyOf(Myself),CLERIC_ALL)
    Class(FourthNearestEnemyOf(Myself),MAGE_ALL)
  See(FourthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(ThirdNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(ThirdNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStatGT(ThirdNearestEnemyOf(Myself),0,HELD)
  !StateCheck(ThirdNearestEnemyOf(Myself),STATE_SILENCED)
  CheckStat(ThirdNearestEnemyOf(Myself),0,SANCTUARY)
  OR(4)
    Class(ThirdNearestEnemyOf(Myself),DRUID_ALL)
    Class(ThirdNearestEnemyOf(Myself),BARD_ALL)
    Class(ThirdNearestEnemyOf(Myself),CLERIC_ALL)
    Class(ThirdNearestEnemyOf(Myself),MAGE_ALL)  
  See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(SecondNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(SecondNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStatGT(SecondNearestEnemyOf(Myself),0,HELD)
  !StateCheck(SecondNearestEnemyOf(Myself),STATE_SILENCED)
  CheckStat(SecondNearestEnemyOf(Myself),0,SANCTUARY)
  OR(4)
    Class(SecondNearestEnemyOf(Myself),DRUID_ALL)
    Class(SecondNearestEnemyOf(Myself),BARD_ALL)
    Class(SecondNearestEnemyOf(Myself),CLERIC_ALL)
    Class(SecondNearestEnemyOf(Myself),MAGE_ALL)  
  See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(NearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(NearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStatGT(NearestEnemyOf(Myself),0,HELD)
  !StateCheck(NearestEnemyOf(Myself),STATE_SILENCED)
  CheckStat(NearestEnemyOf(Myself),0,SANCTUARY)
  OR(4)
    Class(NearestEnemyOf(Myself),DRUID_ALL)
    Class(NearestEnemyOf(Myself),BARD_ALL)
    Class(NearestEnemyOf(Myself),CLERIC_ALL)
    Class(NearestEnemyOf(Myself),MAGE_ALL)  
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  GlobalTimerExpired("ja#round","LOCALS")
  !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(LastSeenBy(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStatGT(LastSeenBy(Myself),0,HELD)
  !StateCheck(LastSeenBy(Myself),STATE_SILENCED)
  CheckStat(LastSeenBy(Myself),0,SANCTUARY)
  OR(4)
    Class(LastSeenBy(Myself),DRUID_ALL)
    Class(LastSeenBy(Myself),BARD_ALL)
    Class(LastSeenBy(Myself),CLERIC_ALL)
    Class(LastSeenBy(Myself),MAGE_ALL)  
  See(LastSeenBy(Myself))  
  RandomNum(4,1)
THEN
  RESPONSE #100
    SetGlobalTimer("ja#round","LOCALS",6)
    ForceSpell(LastSeenBy(Myself),TANARI_SILENCE)
END


/////////////////////////////////////////////////////////////////////
// Paralyze 10 feet
/////////////////////////////////////////////////////////////////////
IF
  !CheckStat(SixthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(SixthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStatGT(SixthNearestEnemyOf(Myself),0,HELD)
  CheckStat(SixthNearestEnemyOf(Myself),0,SANCTUARY)
  See(SixthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(FifthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(FifthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStatGT(FifthNearestEnemyOf(Myself),0,HELD)
  CheckStat(FifthNearestEnemyOf(Myself),0,SANCTUARY)
  See(FifthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(FourthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(FourthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStatGT(FourthNearestEnemyOf(Myself),0,HELD)
  CheckStat(FourthNearestEnemyOf(Myself),0,SANCTUARY)
  See(FourthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(ThirdNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(ThirdNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStatGT(ThirdNearestEnemyOf(Myself),0,HELD)
  CheckStat(ThirdNearestEnemyOf(Myself),0,SANCTUARY)
  See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(SecondNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(SecondNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStatGT(SecondNearestEnemyOf(Myself),0,HELD)
  CheckStat(SecondNearestEnemyOf(Myself),0,SANCTUARY)
  See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(NearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(NearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStatGT(NearestEnemyOf(Myself),0,HELD)
  CheckStat(NearestEnemyOf(Myself),0,SANCTUARY)
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  GlobalTimerExpired("ja#round","LOCALS")
  !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(LastSeenBy(Myself),1,POTION_OF_MAGIC_SHIELDING)
  !CheckStatGT(LastSeenBy(Myself),0,HELD)
  CheckStat(LastSeenBy(Myself),0,SANCTUARY)
  See(LastSeenBy(Myself))
  RandomNum(4,2)
THEN
  RESPONSE #100
    SetGlobalTimer("ja#round","LOCALS",6)
    ForceSpellRES("JA#tana2",LastSeenBy(Myself))
END


/////////////////////////////////////////////////////////////////////
// Energy Drain
/////////////////////////////////////////////////////////////////////
IF
  !CheckStat(SixthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(SixthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  CheckStat(SixthNearestEnemyOf(Myself),0,SANCTUARY)
  See(SixthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(FifthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(FifthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  CheckStat(FifthNearestEnemyOf(Myself),0,SANCTUARY)
  See(FifthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(FourthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(FourthNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  CheckStat(FourthNearestEnemyOf(Myself),0,SANCTUARY)
  See(FourthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(ThirdNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(ThirdNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  CheckStat(ThirdNearestEnemyOf(Myself),0,SANCTUARY)
  See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(SecondNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(SecondNearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  CheckStat(SecondNearestEnemyOf(Myself),0,SANCTUARY)
  See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !CheckStat(NearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(NearestEnemyOf(Myself),1,POTION_OF_MAGIC_SHIELDING)
  CheckStat(NearestEnemyOf(Myself),0,SANCTUARY)
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  GlobalTimerExpired("ja#round","LOCALS")
  !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  !CheckStat(LastSeenBy(Myself),1,POTION_OF_MAGIC_SHIELDING)
  CheckStat(LastSeenBy(Myself),0,SANCTUARY)
  See(LastSeenBy(Myself))
  RandomNum(4,3)
THEN
  RESPONSE #100
    SetGlobalTimer("ja#round","LOCALS",6)
    ForceSpell(LastSeenBy(Myself),SUCCUBUS_ENERGY_DRAIN)
END


/////////////////////////////////////////////////////////////////////
// Vampiric touch
/////////////////////////////////////////////////////////////////////
IF
  Range(SixthNearestEnemyOf(Myself),5)
  !CheckStat(SixthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(SixthNearestEnemyOf(Myself),0,SANCTUARY)
  See(SixthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Range(FifthNearestEnemyOf(Myself),5)
  !CheckStat(FifthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(FifthNearestEnemyOf(Myself),0,SANCTUARY)
  See(FifthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Range(FourthNearestEnemyOf(Myself),5)
  !CheckStat(FourthNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(FourthNearestEnemyOf(Myself),0,SANCTUARY)
  See(FourthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Range(ThirdNearestEnemyOf(Myself),5)
  !CheckStat(ThirdNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(ThirdNearestEnemyOf(Myself),0,SANCTUARY)
  See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Range(SecondNearestEnemyOf(Myself),5)
  !CheckStat(SecondNearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(SecondNearestEnemyOf(Myself),0,SANCTUARY)
  See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Range(NearestEnemyOf(Myself),5)
  !CheckStat(NearestEnemyOf(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(NearestEnemyOf(Myself),0,SANCTUARY)
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  GlobalTimerExpired("ja#round","LOCALS")
  Range(LastSeenBy(Myself),5)
  !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE3) // Protection from magic
  CheckStat(LastSeenBy(Myself),0,SANCTUARY)
  See(LastSeenBy(Myself))
THEN
  RESPONSE #100
    SetGlobalTimer("ja#round","LOCALS",6)
    ForceSpellRES("JA#tana3",LastSeenBy(Myself))
END


/////////////////////////////////////////////////////////////////////
// Find a helpless target to attack
/////////////////////////////////////////////////////////////////////
IF
  CheckStat(SixthNearestEnemyOf(Myself),0,SANCTUARY)
  CheckStatGT(SixthNearestEnemyOf(Myself),0,HELD)
  See(SixthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(FifthNearestEnemyOf(Myself),0,SANCTUARY)
  CheckStatGT(FifthNearestEnemyOf(Myself),0,HELD)
  See(FifthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(FourthNearestEnemyOf(Myself),0,SANCTUARY)
  CheckStatGT(FourthNearestEnemyOf(Myself),0,HELD)
  See(FourthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(ThirdNearestEnemyOf(Myself),0,SANCTUARY)
  CheckStatGT(ThirdNearestEnemyOf(Myself),0,HELD)
  See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(SecondNearestEnemyOf(Myself),0,SANCTUARY)
  CheckStatGT(SecondNearestEnemyOf(Myself),0,HELD)
  See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  CheckStat(NearestEnemyOf(Myself),0,SANCTUARY)  
  CheckStatGT(NearestEnemyOf(Myself),0,HELD)
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  //!GlobalTimerNotExpired("ja#attack","LOCALS")
  CheckStat(LastSeenBy(Myself),0,SANCTUARY)  
  CheckStatGT(LastSeenBy(Myself),0,HELD)
  See(LastSeenBy(Myself))
THEN
  RESPONSE #100
    //SetGlobalTimer("ja#attack","LOCALS",6)
    AttackOneRound(LastSeenBy(Myself))
END


/////////////////////////////////////////////////////////////////////
// Random attack (priority on melee range target)
/////////////////////////////////////////////////////////////////////

// three melee target
IF
  Range(ThirdNearestEnemyOf(Myself),8)
  RandomNum(6,1)
  OR(3)
    See(NearestEnemyOf(Myself))
    See(SecondNearestEnemyOf(Myself))
    See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Range(ThirdNearestEnemyOf(Myself),8)
  RandomNum(6,2)
  OR(3)
    See(SecondNearestEnemyOf(Myself))
    See(NearestEnemyOf(Myself))
    See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Range(ThirdNearestEnemyOf(Myself),8)
  RandomNum(6,3)
  OR(3)
    See(NearestEnemyOf(Myself))
    See(ThirdNearestEnemyOf(Myself))
    See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Range(ThirdNearestEnemyOf(Myself),8)
  RandomNum(6,4)
  OR(3)
    See(ThirdNearestEnemyOf(Myself))
    See(NearestEnemyOf(Myself))
    See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Range(ThirdNearestEnemyOf(Myself),8)
  RandomNum(6,5)
  OR(3)
    See(SecondNearestEnemyOf(Myself))
    See(ThirdNearestEnemyOf(Myself))
    See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Range(ThirdNearestEnemyOf(Myself),8)
  RandomNum(6,6)
  OR(3)
    See(ThirdNearestEnemyOf(Myself))
    See(SecondNearestEnemyOf(Myself))
    See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

// two melee target
IF
  !Range(ThirdNearestEnemyOf(Myself),8)
  Range(SecondNearestEnemyOf(Myself),8)
  RandomNum(2,1)
  OR(2)
    See(NearestEnemyOf(Myself))
    See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !Range(ThirdNearestEnemyOf(Myself),8)
  Range(SecondNearestEnemyOf(Myself),8)
  RandomNum(2,2)
  OR(2)
    See(SecondNearestEnemyOf(Myself))
    See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

// one melee target
IF
  !Range(SecondNearestEnemyOf(Myself),8)
  Range(NearestEnemyOf(Myself),8)
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Range(LastSeenBy(Myself),8)
  See(LastSeenBy(Myself))
  CheckStat(LastSeenBy(Myself),0,SANCTUARY)
  //!GlobalTimerNotExpired("ja#attack","LOCALS")
THEN
  RESPONSE #100
    //SetGlobalTimer("ja#attack","LOCALS",6)
    AttackOneRound(LastSeenBy(Myself))
END


// No target in melee range, pick a random one
IF
  RandomNum(8,1)
  See(NearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  See(FourthNearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  See(SixthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  RandomNum(8,2)
  See(FourthNearestEnemyOf(Myself))
  See(NearestEnemyOf(Myself))
  See(SixthNearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  RandomNum(8,3)
  See(SixthNearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  See(NearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  See(FourthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  RandomNum(8,4)
  See(FourthNearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  See(SixthNearestEnemyOf(Myself))
  See(NearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  RandomNum(8,5)
  See(SixthNearestEnemyOf(Myself))
  See(FourthNearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  See(NearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  RandomNum(8,6)
  See(SixthNearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  See(FourthNearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  RandomNum(8,7)
  See(SixthNearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  See(FourthNearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  See(NearestEnemyOf(Myself))
  See(WeakestOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  RandomNum(8,8)
  See(SixthNearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  See(FourthNearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  See(NearestEnemyOf(Myself))
  See(MostDamagedOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !Range(LastSeenBy(Myself),5)
  CheckStat(LastSeenBy(Myself),0,SANCTUARY)
  //!GlobalTimerNotExpired("ja#attack","LOCALS")
THEN
  RESPONSE #100
    //SetGlobalTimer("ja#attack","LOCALS",6)
    AttackOneRound(LastSeenBy(Myself))
END


/////////////////////////////////////////////////////////////////////
// Nothing to do, teleport to someone
/////////////////////////////////////////////////////////////////////
IF
  ActionListEmpty()
  Global("ja#nothingtodo","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("ja#nothingtodo","LOCALS",1)
END