///////////////////////////////////////////////////////////////////////
// Add kit special abilities (not added for npc)
///////////////////////////////////////////////////////////////////////


/////////////////////////////////////////////////////////////////////
// Cavalier
/////////////////////////////////////////////////////////////////////
IF
	Global("ja#kitabilities","LOCALS",0)
	Kit(Myself,CAVALIER)
THEN
	RESPONSE #100
		SetGlobal("ja#kitabilities","LOCALS",1)
		ReallyForceSpellRES("spcl221",Myself)
END


/////////////////////////////////////////////////////////////////////
// Inquisitor
/////////////////////////////////////////////////////////////////////
IF
	Global("ja#kitabilities","LOCALS",0)
	Kit(Myself,INQUISITOR)
THEN
	RESPONSE #100
		SetGlobal("ja#kitabilities","LOCALS",1)
		ReallyForceSpellRES("spcl233b",Myself)
END


/////////////////////////////////////////////////////////////////////
// Xvim
/////////////////////////////////////////////////////////////////////
IF
	Global("ja#kitabilities","LOCALS",0)
	Kit(Myself,CDXvim)
THEN
	RESPONSE #100
		SetGlobal("ja#kitabilities","LOCALS",1)
		ReallyForceSpellRES("cdxvfear",Myself)
END


/////////////////////////////////////////////////////////////////////
// Deathbringer
/////////////////////////////////////////////////////////////////////
IF
	Global("ja#kitabilities","LOCALS",0)
	Kit(Myself,IK_DEATHBRINGER)
THEN
	RESPONSE #100
		SetGlobal("ja#kitabilities","LOCALS",1)
		ReallyForceSpellRES("ikdb1",Myself)
END


/////////////////////////////////////////////////////////////////////
// Monk
/////////////////////////////////////////////////////////////////////

// Level 5 Immunity to all diseases, and cannot be Slowed or Hasted.
IF
	Global("ja#kitabilities","LOCALS",0)
	Class(Myself,20)
	CheckStatGT(Myself,4,LEVEL)
THEN
	RESPONSE #100
		SetGlobal("ja#kitabilities","LOCALS",1)
		ReallyForceSpellRES("SPCL814",Myself)
		Continue()
END

// Level 9 Immune to charm.
IF
	Global("ja#kitabilities","LOCALS",0)
	Class(Myself,20)
	CheckStatGT(Myself,8,LEVEL)
THEN
	RESPONSE #100
		SetGlobal("ja#kitabilities","LOCALS",1)
		ReallyForceSpellRES("SPCL817",Myself)
		Continue()
END

// Level 11 Immune to poison.
IF
	Global("ja#kitabilities","LOCALS",0)
	Class(Myself,20)
	CheckStatGT(Myself,10,LEVEL)
THEN
	RESPONSE #100
		SetGlobal("ja#kitabilities","LOCALS",1)
		ReallyForceSpellRES("SPCL818",Myself)
		Continue()
END

// Level 20 Immune to non-magical weapons
IF
	Global("ja#kitabilities","LOCALS",0)
	Class(Myself,20)
	CheckStatGT(Myself,19,LEVEL)
THEN
	RESPONSE #100
		SetGlobal("ja#kitabilities","LOCALS",1)
		ReallyForceSpellRES("SPCL821",Myself)
		Continue()
END
///////////////////////////////////////////////////////////////////////
// Initialization script
// - sleep
// - initialization
///////////////////////////////////////////////////////////////////////


/////////////////////////////////////////////////////////////////////
// Just after resting, everything is reset and creature is healed
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("ja#PrepTimer","LOCALS")
	!Global("ja#initialize","LOCALS",0)
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		Rest()
		ApplySpellRES("JA#HEAL",Myself)
		SetGlobal("ja#initialize","LOCALS",0)
END


///////////////////////////////////////////////////////////////////////
// Initialize variables & timers
///////////////////////////////////////////////////////////////////////
IF
	Global("ja#initialize","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("ja#initialize","LOCALS",1)
		SetGlobal("ja#startcombat","LOCALS",0)
		SetGlobalTimer("ja#PrepTimer","LOCALS",2400)
END


///////////////////////////////////////////////////////////////////////
// Threat Detection for mages
// - turn hostile if the mage or his summons are being attacked
// - alert nearby allies
///////////////////////////////////////////////////////////////////////


///////////////////////////////////////////////////////////////////////
// Attacked? Spell cast on me? Someone shouted a warning?
///////////////////////////////////////////////////////////////////////
IF
	!Allegiance(Myself,ENEMY)
	OR(12)
		AttackedBy([GOODCUTOFF.0.0.0.0.SUMMONED],DEFAULT)
		AttackedBy([PC],DEFAULT)
		AttackedBy([FAMILIAR],DEFAULT)
		AttackedBy([ALLY],DEFAULT)
		AttackedBy([CONTROLLED],DEFAULT)
		AttackedBy([CHARMED],DEFAULT)
		SpellCastOnMe([GOODCUTOFF.0.0.0.0.SUMMONED],0)
		SpellCastOnMe([PC],0)
		SpellCastOnMe([FAMILIAR],0)
		SpellCastOnMe([ALLY],0)
		SpellCastOnMe([CONTROLLED],0)
		SpellCastOnMe([CHARMED],0)
THEN
	RESPONSE #100
		Enemy()
END

// Summons are beeing attack
IF
	Heard([ANYONE],98)
	Allegiance(Myself,NEUTRAL)
THEN
	RESPONSE #100
		Enemy()
END

// Adventurers are beeing attack
IF
	Heard([ANYONE],97)
	Allegiance(Myself,NEUTRAL)
THEN
	RESPONSE #100
		Enemy()
		//MoveToObject(LastHeardBy(Myself))
		SetGlobal("ja#startcombat","LOCALS",1)
END

IF
	Heard([ANYONE],97)
	!See(NearestEnemyOf(Myself))	
THEN
	RESPONSE #100
		//MoveToObject(LastHeardBy(Myself))
		SetGlobal("ja#startcombat","LOCALS",1)
END


/////////////////////////////////////////////////////////////////////
// Alert nearby allies if an enemy is spotted
/////////////////////////////////////////////////////////////////////
IF
	See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("ja#shout","LOCALS")
THEN
	RESPONSE #100
		SetGlobal("ja#startcombat","LOCALS",1)
		VerbalConstant(Myself,BATTLE_CRY)
		SetGlobalTimer("ja#shout","LOCALS",30)
		Shout(97)
END


/////////////////////////////////////////////////////////////////////
// Lay on hands
/////////////////////////////////////////////////////////////////////
IF
	!GlobalTimerNotExpired("ja#cast","LOCALS")
	HPPercentLT(Myself,60)
	HaveSpellRES("spcl211")
THEN
	RESPONSE #100
		SetGlobalTimer("ja#cast","LOCALS",6)
		SpellRES("spcl211",Myself)
END

IF
	!GlobalTimerNotExpired("ja#cast","LOCALS")
	HaveSpellRES("spcl815")
	HPPercentLT(Myself,60)
THEN
	RESPONSE #100
		SetGlobalTimer("ja#cast","LOCALS",6)
		SpellRES("spcl815",Myself)
END


/////////////////////////////////////////////////////////////////////
// Healing potions
/////////////////////////////////////////////////////////////////////
IF
	!GlobalTimerNotExpired("ja#cast","LOCALS")
	HasItem("POTN55",Myself) // Potion of Superior Healing
	HPPercentLT(Myself,50)
THEN
	RESPONSE #100
		SetGlobalTimer("ja#cast","LOCALS",6)
		UseItem("POTN55",Myself)
END

IF
	!GlobalTimerNotExpired("ja#cast","LOCALS")
	HasItem("POTN52",Myself) // Potion of Extra Healing
	HPPercentLT(Myself,60)
THEN
	RESPONSE #100
		SetGlobalTimer("ja#cast","LOCALS",6)
		UseItem("POTN52",Myself)
END

IF
	!GlobalTimerNotExpired("ja#cast","LOCALS")
	HasItem("_POTN17",Myself) // Elixir of Health
	OR(2)
		HPPercentLT(Myself,30)
		StateCheck(Myself,STATE_POISONED)
THEN
	RESPONSE #100
		SetGlobalTimer("ja#cast","LOCALS",6)
		UseItem("_POTN17",Myself)
END

IF
	!GlobalTimerNotExpired("ja#cast","LOCALS")
	HasItem("_POTN08",Myself) // Potion of Healing
	HPPercentLT(Myself,30)
THEN
	RESPONSE #100
		SetGlobalTimer("ja#cast","LOCALS",6)
		UseItem("_POTN08",Myself)
END


///////////////////////////////////////////////////////////////////////
// Movement and Tracking
///////////////////////////////////////////////////////////////////////


/////////////////////////////////////////////////////////////////////
// Thieves will try to hide
/////////////////////////////////////////////////////////////////////
IF
	OR(2)
		Class(Myself,THIEF)
		Class(Myself,THIEF_ALL)
	!See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_INVISIBLE)
	!GlobalTimerNotExpired("ja#stealth","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("ja#stealth","LOCALS",9)
		Hide()
END

IF
	OR(2)
		Class(Myself,THIEF)
		Class(Myself,THIEF_ALL)
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_INVISIBLE)
	!GlobalTimerNotExpired("Stealth","LOCALS")
	!RandomNum(4,1) // Sometimes, it's good to fight
THEN
	RESPONSE #100
		RunAwayFromNoInterrupt(NearestEnemyOf(Myself),45)
END

// If thief is not invisible, he won't track down an enemy not in sight
IF
	OR(2)
		Class(Myself,THIEF)
		Class(Myself,THIEF_ALL)
	!See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_INVISIBLE)
THEN
	RESPONSE #100
END


/////////////////////////////////////////////////////////////////////
// Are there any enemies that try to escape, follow them
// It's not about sight but memory (I saw that enemy going in that dir.)
/////////////////////////////////////////////////////////////////////
IF
	Global("ja#startcombat","LOCALS",1)
	!See(NearestEnemyOf(Myself))
	!StateCheck(LastAttackerOf(Myself),STATE_INVISIBLE)
	Range(LastAttackerOf(Myself),70)
	OR(2)
		!Class(Myself,THIEF_ALL)
		StateCheck(Myself,STATE_INVISIBLE)
THEN
	RESPONSE #100
		MoveToObject(LastAttackerOf(Myself))
END

IF
	Global("ja#startcombat","LOCALS",1)
	Allegiance(Myself,ENEMY)
	!See(NearestEnemyOf(Myself))
	!StateCheck(Player1,STATE_INVISIBLE)
	Range(Player1,70)
	OR(2)
		!Class(Myself,THIEF_ALL)
		StateCheck(Myself,STATE_INVISIBLE)
THEN
	RESPONSE #100
		MoveToObject(Player1)
END

IF
	Global("ja#startcombat","LOCALS",1)
	Allegiance(Myself,ENEMY)
	!See(NearestEnemyOf(Myself))
	!StateCheck(Player2,STATE_INVISIBLE)
	Range(Player2,70)
	OR(2)
		!Class(Myself,THIEF_ALL)
		StateCheck(Myself,STATE_INVISIBLE)
THEN
	RESPONSE #100
		MoveToObject(Player2)
END

IF
	Global("ja#startcombat","LOCALS",1)
	Allegiance(Myself,ENEMY)
	!See(NearestEnemyOf(Myself))
	!StateCheck(Player3,STATE_INVISIBLE)
	Range(Player3,70)
	OR(2)
		!Class(Myself,THIEF_ALL)
		StateCheck(Myself,STATE_INVISIBLE)
THEN
	RESPONSE #100
		MoveToObject(Player3)
END

IF
	Global("ja#startcombat","LOCALS",1)
	Allegiance(Myself,ENEMY)
	!See(NearestEnemyOf(Myself))
	!StateCheck(Player4,STATE_INVISIBLE)
	Range(Player4,70)
	OR(2)
		!Class(Myself,THIEF_ALL)
		StateCheck(Myself,STATE_INVISIBLE)
THEN
	RESPONSE #100
		MoveToObject(Player4)
END

IF
	Global("ja#startcombat","LOCALS",1)
	Allegiance(Myself,ENEMY)
	!See(NearestEnemyOf(Myself))
	!StateCheck(Player5,STATE_INVISIBLE)
	Range(Player5,70)
	OR(2)
		!Class(Myself,THIEF_ALL)
		StateCheck(Myself,STATE_INVISIBLE)
THEN
	RESPONSE #100
		MoveToObject(Player5)
END

IF
	Global("ja#startcombat","LOCALS",1)
	Allegiance(Myself,ENEMY)
	!See(NearestEnemyOf(Myself))
	!StateCheck(Player6,STATE_INVISIBLE)
	Range(Player6,70)
	OR(2)
		!Class(Myself,THIEF_ALL)
		StateCheck(Myself,STATE_INVISIBLE)
THEN
	RESPONSE #100
		MoveToObject(Player6)
END


///////////////////////////////////////////////////////////////////////
// Behavior if no enemies have been seen
///////////////////////////////////////////////////////////////////////
IF
	!See(NearestEnemyOf(Myself))
	Global("ja#randomwalk","LOCALS",1)
THEN
	RESPONSE #100
		RandomWalk()
END

IF
	!See(NearestEnemyOf(Myself))
	Global("ja#randomwalk","LOCALS",0)
THEN
	RESPONSE #100
		NoAction()
END


/////////////////////////////////////////////////////////////////////
// Potions of strength
/////////////////////////////////////////////////////////////////////
IF
	!GlobalTimerNotExpired("ja#cast","LOCALS")
	HasItem("_POTN04",Myself) // Potion of Frost Giant Strength
	CheckStatLT(Myself,19,STR)
THEN
	RESPONSE #100
		SetGlobalTimer("DrinkPotion","LOCALS",6)
		UseItem("_POTN04",Myself)
END

IF
	!GlobalTimerNotExpired("ja#cast","LOCALS")
	HasItem("_POTN12",Myself) // Potion of Stone Giant Strength
	CheckStatLT(Myself,19,STR)
THEN
	RESPONSE #100
		SetGlobalTimer("ja#cast","LOCALS",6)
		UseItem("_POTN12",Myself)
END


/////////////////////////////////////////////////////////////////////
// Other potions
/////////////////////////////////////////////////////////////////////
IF
	!GlobalTimerNotExpired("ja#cast","LOCALS")
	!StateCheck(Myself,STATE_INVISIBLE)	
	HasItem("_POTN10",Myself) // Potion of Invisibility
THEN
	RESPONSE #100
		SetGlobalTimer("ja#cast","LOCALS",6)
		UseItem("_POTN10",Myself)
END

IF
	!GlobalTimerNotExpired("ja#cast","LOCALS")
	HasItem("_POTN18",Myself) // Potion of Absorption
THEN
	RESPONSE #100
		SetGlobalTimer("ja#cast","LOCALS",6)
		UseItem("_POTN18",Myself)
END

IF
	!GlobalTimerNotExpired("ja#cast","LOCALS")
	HasItem("_POTN09",Myself) // Potion of Heroism
THEN
	RESPONSE #100
		SetGlobalTimer("ja#cast","LOCALS",6)
		UseItem("_POTN09",Myself)
END

IF
	!GlobalTimerNotExpired("ja#cast","LOCALS")
	HasItem("_POTN14",Myself) // Oil of Speed
	!StateCheck(Myself,STATE_HASTED)
THEN
	RESPONSE #100
		SetGlobalTimer("ja#cast","LOCALS",6)
		UseItem("_POTN14",Myself)
END

IF
	!GlobalTimerNotExpired("ja#cast","LOCALS")
	HasItem("_POTN34",Myself) // Potion of Magic Protection
THEN
	RESPONSE #100
		SetGlobalTimer("ja#cast","LOCALS",6)
		UseItem("_POTN34",LastSeenBy(Myself))
END

IF
	!GlobalTimerNotExpired("ja#cast","LOCALS")
	HasItem("_POTN41",Myself) // Potion of Power
THEN
	RESPONSE #100
		SetGlobalTimer("ja#cast","LOCALS",6)
		UseItem("_POTN41",Myself)
END

IF
	!GlobalTimerNotExpired("ja#cast","LOCALS")
	HasItem("_POTN36",Myself) // Potion of Master Thievery
THEN
	RESPONSE #100
		SetGlobalTimer("ja#cast","LOCALS",6)
		UseItem("_POTN36",Myself)
END


///////////////////////////////////////////////////////////////////////
// Special kit abilities
///////////////////////////////////////////////////////////////////////


/////////////////////////////////////////////////////////////////////
// Poison weapon
/////////////////////////////////////////////////////////////////////
IF
	!GlobalTimerNotExpired("ja#cast","LOCALS")
	!CheckStat(Myself,4,SCRIPTINGSTATE4)
	HaveSpellRES("spcl423")
THEN
	RESPONSE #100
		SetGlobalTimer("ja#cast","LOCALS",6)
		SpellRES("spcl423",Myself)
END


/////////////////////////////////////////////////////////////////////
// Berserker rage
/////////////////////////////////////////////////////////////////////
IF
	!GlobalTimerNotExpired("ja#cast","LOCALS")
	!GlobalTimerNotExpired("ja#berserkerrage","LOCALS")
	HaveSpellRES("spcl321")
THEN
	RESPONSE #100
		SetGlobalTimer("ja#cast","LOCALS",6)
		SetGlobalTimer("ja#berserkerrage","LOCALS",120)
		SpellRES("spcl321",Myself)
END


/////////////////////////////////////////////////////////////////////
// Barbarian rage
/////////////////////////////////////////////////////////////////////
IF
	!GlobalTimerNotExpired("ja#cast","LOCALS")
	!CheckStat(Myself,1,SCRIPTINGSTATE4)
	HaveSpellRES("spcl152")
THEN
	RESPONSE #100
		SetGlobalTimer("ja#cast","LOCALS",6)
		SpellRES("spcl152",Myself)
END


/////////////////////////////////////////////////////////////////////
// Kai
/////////////////////////////////////////////////////////////////////
IF
	!GlobalTimerNotExpired("ja#cast","LOCALS")
	!GlobalTimerNotExpired("ja#kai","LOCALS")
	HaveSpellRES("spcl144")
THEN
	RESPONSE #100
		SetGlobalTimer("ja#cast","LOCALS",6)
		SetGlobalTimer("ja#kai","LOCALS",10)
		SpellRES("spcl144",Myself)
END


/////////////////////////////////////////////////////////////////////
// Called shot
/////////////////////////////////////////////////////////////////////
IF
	!GlobalTimerNotExpired("ja#cast","LOCALS")
	!GlobalTimerNotExpired("ja#calledshot","LOCALS")
	HaveSpellRES("spcl121")
THEN
	RESPONSE #100
		SetGlobalTimer("ja#cast","LOCALS",6)
		SetGlobalTimer("ja#calledshot","LOCALS",10)
		SpellRES("spcl121",Myself)
END


/////////////////////////////////////////////////////////////////////
// Protection from evil
/////////////////////////////////////////////////////////////////////
IF
	!GlobalTimerNotExpired("ja#cast","LOCALS")
	!CheckStat(Myself,3,SCRIPTINGSTATE3)
	HaveSpellRES("spcl213")
THEN
	RESPONSE #100
		SetGlobalTimer("ja#cast","LOCALS",6)
		SpellRES("spcl213",Myself)
END


/////////////////////////////////////////////////////////////////////
// True sight
/////////////////////////////////////////////////////////////////////
IF
	!GlobalTimerNotExpired("ja#cast","LOCALS")
	Global("ja#disable_spellcasting","LOCALS",0)
	CheckStat(Myself,0,TRUE_SIGHT)
	HaveSpellRES("spcl232")
THEN
	RESPONSE #100
		SetGlobalTimer("ja#cast","LOCALS",6)
		SpellRES("spcl232",Myself)
END


/////////////////////////////////////////////////////////////////////
// Dispel magic
/////////////////////////////////////////////////////////////////////
IF
	!GlobalTimerNotExpired("ja#cast","LOCALS")
	Global("ja#disable_spellcasting","LOCALS",0)
	HaveSpellRES("spcl231")
THEN
	RESPONSE #100
		SetGlobalTimer("ja#cast","LOCALS",6)
		SpellRES("spcl231",NearestEnemyOf(Myself))
END


/////////////////////////////////////////////////////////////////////
// Defensive spin
/////////////////////////////////////////////////////////////////////
IF
	!GlobalTimerNotExpired("ja#cast","LOCALS")
	!CheckStat(Myself,2,SCRIPTINGSTATE4)
	HitBy([ANYONE],CRUSHING)
	HaveSpellRES("spcl522")
THEN
	RESPONSE #100
		SetGlobalTimer("ja#cast","LOCALS",6)
		SpellRES("spcl522",Myself)
END


/////////////////////////////////////////////////////////////////////
// Offensive spin
/////////////////////////////////////////////////////////////////////
IF
	!GlobalTimerNotExpired("ja#cast","LOCALS")
	!CheckStat(Myself,2,SCRIPTINGSTATE4)
	Global("ja#buffdone","LOCALS",1)
	HaveSpellRES("spcl521")
THEN
	RESPONSE #100
		SetGlobalTimer("ja#cast","LOCALS",6)
		SpellRES("spcl521",Myself)
END


/////////////////////////////////////////////////////////////////////
// Stunning Blow
/////////////////////////////////////////////////////////////////////
IF
	!GlobalTimerNotExpired("ja#cast","LOCALS")
	!GlobalTimerNotExpired("ja#stunning","LOCALS")
	HaveSpellRES("spcl811")
THEN
	RESPONSE #100
		SetGlobalTimer("ja#cast","LOCALS",6)
		SetGlobalTimer("ja#stunning","LOCALS",9)
		SpellRES("spcl811",Myself)
END


/////////////////////////////////////////////////////////////////////
// Quivering Palm
/////////////////////////////////////////////////////////////////////
IF
	!GlobalTimerNotExpired("ja#cast","LOCALS")
	HaveSpellRES("spcl820")
THEN
	RESPONSE #100
		SetGlobalTimer("ja#cast","LOCALS",6)
		SpellRES("spcl820",Myself)
END


///////////////////////////////////////////////////////////////////////
// Fighting with a melee weapon only, for NPC
///////////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////////
// Find a slept target. this only work for PC's enemies            //
// because sleeping creatures are not considered as enemies        //
/////////////////////////////////////////////////////////////////////
IF
	!StateCheck(Player6,STATE_CHARMED)
	CheckStat(Player6,0,SANCTUARY)
	StateCheck(Player6,STATE_SLEEPING)
	See(Player6)
	False()
THEN
	RESPONSE #100
END

IF
	!StateCheck(Player5,STATE_CHARMED)
	CheckStat(Player5,0,SANCTUARY)
	StateCheck(Player5,STATE_SLEEPING)
	See(Player5)
	False()
THEN
	RESPONSE #100
END

IF
	!StateCheck(Player4,STATE_CHARMED)
	CheckStat(Player4,0,SANCTUARY)
	StateCheck(Player4,STATE_SLEEPING)
	See(Player4)
	False()
THEN
	RESPONSE #100
END

IF
	!StateCheck(Player3,STATE_CHARMED)
	CheckStat(Player3,0,SANCTUARY)
	StateCheck(Player3,STATE_SLEEPING)
	See(Player3)
	False()
THEN
	RESPONSE #100
END

IF
	!StateCheck(Player2,STATE_CHARMED)
	CheckStat(Player2,0,SANCTUARY)
	StateCheck(Player2,STATE_SLEEPING)
	See(Player2)
	False()
THEN
	RESPONSE #100
END

IF
	!StateCheck(Player1,STATE_CHARMED)
	CheckStat(Player1,0,SANCTUARY)
	StateCheck(Player1,STATE_SLEEPING)
	See(Player1)
	False()
THEN
	RESPONSE #100
END

IF
	Allegiance(Myself,Enemy)
	!StateCheck(LastSeenBy(Myself),STATE_CHARMED)
	CheckStat(LastSeenBy(Myself),0,SANCTUARY)	
	StateCheck(LastSeenBy(Myself),STATE_SLEEPING)
	See(LastSeenBy(Myself))
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		AttackOneRound(LastSeenBy(Myself))
END	


/////////////////////////////////////////////////////////////////////
// Do nothing if no enemies are seen
/////////////////////////////////////////////////////////////////////
IF
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		NoAction()
END


/////////////////////////////////////////////////////////////////////
// Find a helpless target to attack
/////////////////////////////////////////////////////////////////////
IF
	CheckStat(SixthNearestEnemyOf(Myself),0,SANCTUARY)
	OR(2)
		StateCheck(SixthNearestEnemyOf(Myself),STATE_STUNNED)
		CheckStatGT(SixthNearestEnemyOf(Myself),0,HELD)
	See(SixthNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
END

IF
	CheckStat(FifthNearestEnemyOf(Myself),0,SANCTUARY)
	OR(2)
		StateCheck(FifthNearestEnemyOf(Myself),STATE_STUNNED)
		CheckStatGT(FifthNearestEnemyOf(Myself),0,HELD)
	See(FifthNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
END

IF
	CheckStat(FourthNearestEnemyOf(Myself),0,SANCTUARY)
	OR(2)
		StateCheck(FourthNearestEnemyOf(Myself),STATE_STUNNED)
		CheckStatGT(FourthNearestEnemyOf(Myself),0,HELD)
	See(FourthNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
END

IF
	CheckStat(ThirdNearestEnemyOf(Myself),0,SANCTUARY)
	OR(2)
		StateCheck(ThirdNearestEnemyOf(Myself),STATE_STUNNED)
		CheckStatGT(ThirdNearestEnemyOf(Myself),0,HELD)
	See(ThirdNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
END

IF
	CheckStat(SecondNearestEnemyOf(Myself),0,SANCTUARY)
	OR(2)
		StateCheck(SecondNearestEnemyOf(Myself),STATE_STUNNED)
		CheckStatGT(SecondNearestEnemyOf(Myself),0,HELD)
	See(SecondNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
END

IF
	CheckStat(NearestEnemyOf(Myself),0,SANCTUARY)	
	OR(2)
		StateCheck(NearestEnemyOf(Myself),STATE_STUNNED)
		CheckStatGT(NearestEnemyOf(Myself),0,HELD)
	See(NearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
END

IF
	CheckStat(LastSeenBy(Myself),0,SANCTUARY)	
	OR(2)
		StateCheck(LastSeenBy(Myself),STATE_STUNNED)
		CheckStatGT(LastSeenBy(Myself),0,HELD)
	See(LastSeenBy(Myself))	
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		AttackOneRound(LastSeenBy(Myself))
END


/////////////////////////////////////////////////////////////////////
// Find something to attack, which has bonus on to hit
/////////////////////////////////////////////////////////////////////
IF
	CheckStat(SixthNearestEnemyOf(Myself),0,SANCTUARY)
	OR(3)
		StateCheck(SixthNearestEnemyOf(Myself),STATE_SLOWED)
		StateCheck(SixthNearestEnemyOf(Myself),STATE_BLIND)
		StateCheck(SixthNearestEnemyOf(Myself),STATE_HELPLESS)
	See(SixthNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
END

IF
	CheckStat(FifthNearestEnemyOf(Myself),0,SANCTUARY)
	OR(3)
		StateCheck(FifthNearestEnemyOf(Myself),STATE_SLOWED)
		StateCheck(FifthNearestEnemyOf(Myself),STATE_BLIND)
		StateCheck(FifthNearestEnemyOf(Myself),STATE_HELPLESS)
	See(FifthNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
END

IF
	CheckStat(FourthNearestEnemyOf(Myself),0,SANCTUARY)
	OR(3)
		StateCheck(FourthNearestEnemyOf(Myself),STATE_SLOWED)
		StateCheck(FourthNearestEnemyOf(Myself),STATE_BLIND)
		StateCheck(FourthNearestEnemyOf(Myself),STATE_HELPLESS)
	See(FourthNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
END

IF
	CheckStat(ThirdNearestEnemyOf(Myself),0,SANCTUARY)
	OR(3)
		StateCheck(ThirdNearestEnemyOf(Myself),STATE_SLOWED)
		StateCheck(ThirdNearestEnemyOf(Myself),STATE_BLIND)
		StateCheck(ThirdNearestEnemyOf(Myself),STATE_HELPLESS)
	See(ThirdNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
END

IF
	CheckStat(SecondNearestEnemyOf(Myself),0,SANCTUARY)
	OR(3)
		StateCheck(SecondNearestEnemyOf(Myself),STATE_SLOWED)
		StateCheck(SecondNearestEnemyOf(Myself),STATE_BLIND)
		StateCheck(SecondNearestEnemyOf(Myself),STATE_HELPLESS)
	See(SecondNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
END

IF
	CheckStat(NearestEnemyOf(Myself),0,SANCTUARY)	
	OR(3)
		StateCheck(NearestEnemyOf(Myself),STATE_SLOWED)
		StateCheck(NearestEnemyOf(Myself),STATE_BLIND)
		StateCheck(NearestEnemyOf(Myself),STATE_HELPLESS)
	See(NearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
END

IF
	CheckStat(LastSeenBy(Myself),0,SANCTUARY)	
	OR(3)
		StateCheck(LastSeenBy(Myself),STATE_SLOWED)
		StateCheck(LastSeenBy(Myself),STATE_BLIND)
		StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
	See(LastSeenBy(Myself))	
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		AttackOneRound(LastSeenBy(Myself))
END


/////////////////////////////////////////////////////////////////////
// Find something to attack, which has control of itself
/////////////////////////////////////////////////////////////////////
IF
	CheckStat(SixthNearestEnemyOf(Myself),0,SANCTUARY)
	!StateCheck(SixthNearestEnemyOf(Myself),STATE_PANIC)
	!StateCheck(SixthNearestEnemyOf(Myself),STATE_FEEBLEMINDED)
	!StateCheck(SixthNearestEnemyOf(Myself),STATE_CONFUSED)
	See(SixthNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
END

IF
	CheckStat(FifthNearestEnemyOf(Myself),0,SANCTUARY)
	!StateCheck(FifthNearestEnemyOf(Myself),STATE_PANIC)
	!StateCheck(FifthNearestEnemyOf(Myself),STATE_FEEBLEMINDED)
	!StateCheck(FifthNearestEnemyOf(Myself),STATE_CONFUSED)
	See(FifthNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
END

IF
	CheckStat(FourthNearestEnemyOf(Myself),0,SANCTUARY)
	!StateCheck(FourthNearestEnemyOf(Myself),STATE_PANIC)
	!StateCheck(FourthNearestEnemyOf(Myself),STATE_FEEBLEMINDED)
	!StateCheck(FourthNearestEnemyOf(Myself),STATE_CONFUSED)
	See(FourthNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
END

IF
	CheckStat(ThirdNearestEnemyOf(Myself),0,SANCTUARY)
	!StateCheck(ThirdNearestEnemyOf(Myself),STATE_PANIC)
	!StateCheck(ThirdNearestEnemyOf(Myself),STATE_FEEBLEMINDED)
	!StateCheck(ThirdNearestEnemyOf(Myself),STATE_CONFUSED)
	See(ThirdNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
END

IF
	CheckStat(SecondNearestEnemyOf(Myself),0,SANCTUARY)
	!StateCheck(SecondNearestEnemyOf(Myself),STATE_PANIC)
	!StateCheck(SecondNearestEnemyOf(Myself),STATE_FEEBLEMINDED)
	!StateCheck(SecondNearestEnemyOf(Myself),STATE_CONFUSED)
	See(SecondNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
END

IF
	CheckStat(NearestEnemyOf(Myself),0,SANCTUARY)	
	!StateCheck(NearestEnemyOf(Myself),STATE_PANIC)
	!StateCheck(NearestEnemyOf(Myself),STATE_FEEBLEMINDED)
	!StateCheck(NearestEnemyOf(Myself),STATE_CONFUSED)
	See(NearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
END

IF
	CheckStat(LastSeenBy(Myself),0,SANCTUARY)	
	!StateCheck(LastSeenBy(Myself),STATE_PANIC)
	!StateCheck(LastSeenBy(Myself),STATE_FEEBLEMINDED)
	!StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
	See(LastSeenBy(Myself))	
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		AttackOneRound(LastSeenBy(Myself))
END


/////////////////////////////////////////////////////////////////////
// Random attack (priority on melee range target)
/////////////////////////////////////////////////////////////////////

// three melee target
IF
  Range(ThirdNearestEnemyOf(Myself),8)
  RandomNum(6,1)
  OR(3)
  	See(NearestEnemyOf(Myself))
  	See(SecondNearestEnemyOf(Myself))
  	See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Range(ThirdNearestEnemyOf(Myself),8)
  RandomNum(6,2)
  OR(3)
  	See(SecondNearestEnemyOf(Myself))
  	See(NearestEnemyOf(Myself))
  	See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Range(ThirdNearestEnemyOf(Myself),8)
  RandomNum(6,3)
  OR(3)
  	See(NearestEnemyOf(Myself))
  	See(ThirdNearestEnemyOf(Myself))
  	See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Range(ThirdNearestEnemyOf(Myself),8)
  RandomNum(6,4)
  OR(3)
  	See(ThirdNearestEnemyOf(Myself))
  	See(NearestEnemyOf(Myself))
  	See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Range(ThirdNearestEnemyOf(Myself),8)
  RandomNum(6,5)
  OR(3)
  	See(SecondNearestEnemyOf(Myself))
  	See(ThirdNearestEnemyOf(Myself))
  	See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  Range(ThirdNearestEnemyOf(Myself),8)
  RandomNum(6,6)
  OR(3)
  	See(ThirdNearestEnemyOf(Myself))
  	See(SecondNearestEnemyOf(Myself))
  	See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

// two melee target
IF
  !Range(ThirdNearestEnemyOf(Myself),8)
  Range(SecondNearestEnemyOf(Myself),8)
  RandomNum(2,1)
  OR(2)
  	See(NearestEnemyOf(Myself))
  	See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !Range(ThirdNearestEnemyOf(Myself),8)
  Range(SecondNearestEnemyOf(Myself),8)
  RandomNum(2,2)
  OR(2)
  	See(SecondNearestEnemyOf(Myself))
  	See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

// one melee target
IF
  !Range(SecondNearestEnemyOf(Myself),8)
  Range(NearestEnemyOf(Myself),8)
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
	See(LastSeenBy(Myself))
	CheckStat(LastSeenBy(Myself),0,SANCTUARY)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		AttackOneRound(LastSeenBy(Myself))
END


// No target in melee range, pick a random one
IF
  RandomNum(8,1)
  See(NearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  See(FourthNearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  See(SixthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  RandomNum(8,2)
  See(FourthNearestEnemyOf(Myself))
  See(NearestEnemyOf(Myself))
  See(SixthNearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  RandomNum(8,3)
  See(SixthNearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  See(NearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  See(FourthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  RandomNum(8,4)
  See(FourthNearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  See(SixthNearestEnemyOf(Myself))
  See(NearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  RandomNum(8,5)
  See(SixthNearestEnemyOf(Myself))
  See(FourthNearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  See(NearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  RandomNum(8,6)
  See(SixthNearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  See(FourthNearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  RandomNum(8,7)
  See(SixthNearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  See(FourthNearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  See(NearestEnemyOf(Myself))
  See(WeakestOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  RandomNum(8,8)
  See(SixthNearestEnemyOf(Myself))
  See(FifthNearestEnemyOf(Myself))
  See(FourthNearestEnemyOf(Myself))
  See(ThirdNearestEnemyOf(Myself))
  See(SecondNearestEnemyOf(Myself))
  See(NearestEnemyOf(Myself))
  See(MostDamagedOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
	CheckStat(LastSeenBy(Myself),0,SANCTUARY)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		AttackOneRound(LastSeenBy(Myself))
END

